component downloadentry extends panel {
	{
		pos			"240 135"
		size		"250 34"
		layout			LAYOUT_LEFT_RIGHT_FLOW
		layoutMargin	1
	}

	button download
	{
		background		"ui/button_green_verysmall"
		color		"0 .78 0 1"
		selectcolor	"1 1 1 1"
		contentalign	ALIGN_CC
		font		"f_verysmall"
		pos			"0 0"
		size		"190 34"
		string		""
		onClick		{ cmd "web_downloadcgame <node:this.parent.category@integer> <node:this.parent.filename@string> <node:this.parent.userid@integer>;" }
	}

	button delete
	{
		size		"34 34"
		icon 		"icons/delete"
		tooltip		"_Remove this file from the server"
		onClick		{ cmd "web_deletecgame <node:this.parent.category@integer> <node:this.parent.filename@string>;" }
	}

	data filename
	{
		string	""
	}

	data category
	{
		integer	0
	}

	data userid
	{
		integer	0
	}
}

window cgame_download
{
	{
		pos			"256 50"
		size		"512 650"
		dragbutton	true
		background		"ui/popup_alpha_tile"
		string		"_Available files:"
		modal		true
	}

	panel downloadable
	{
		{
			pos		"35 70"
			size	"440 550"
			layout			LAYOUT_TOP_DOWN_FLOW
			layoutMargin	2
		}
	}

	/**
	 * @brief Add a server stored file to the list of downloadable files
	 * @param <1> The file id (string)
	 * @param <2> The category
	 * @param <3> The user id of the owner
	 * @param <4> The file name
	 * @param <5> The title
	 * @param <6> Own file = 1, other users file = 0
	 */
	confunc cgamefiles_add
	{
		call *node:root.downloadable@createChild("download<1>-<2>-<3>", "downloadentry")
		*node:root.downloadable.download<1>-<2>-<3>.category@integer = <2>
		*node:root.downloadable.download<1>-<2>-<3>.userid@integer = <3>
		*node:root.downloadable.download<1>-<2>-<3>.filename@string = <4>
		*node:root.downloadable.download<1>-<2>-<3>.download@string = <5>
		if ( <6> == 1 ) {
			*node:root.downloadable.download<1>-<2>-<3>.delete@invis = false
		} else {
			*node:root.downloadable.download<1>-<2>-<3>.delete@invis = true
		}
	}

	confunc cgamefiles_nofilesfound
	{
		call *node:root.downloadable@createChild("nofiles", "string")
		*node:root.downloadable.nofiles@string = "_No files uploaded yet"
	}

	/**
	 * @brief Called when a file download was successful
	 * @param <1> the filename of the newly available file
	 */
	confunc cgame_downloadsuccessful
	{
	}

	confunc cgame_downloadduplicate
	{
	}

	confunc cgame_uploadsuccessful
	{
	}

	confunc cgame_uploadfailed
	{
	}

	/**
	 * @param <1> The file id (string)
	 * @param <2> The category
	 * @param <3> The user id of the owner
	 */
	confunc cgame_deletesuccessful
	{
		call *node:root.downloadable.download<1>-<2>-<3>@delete
	}

	confunc cgame_deletefailed
	{
	}

	confunc cgamefiles_clear
	{
		call *node:root.downloadable@removeAllChild
	}

	func onWindowOpened
	{
		cmd	"web_listcgame 0;"
	}
}
