// =============
// OPTIONS MENU
// =============

component cmpMenuPageHeader extends string {
	pos			"55 67"
	size		"465 35"
	font		"f_menubig"
	contentalign	ALIGN_CC
	color		"0 0.5 0 1"
}

component cmpKeyBinding extends panel {
	{
		size 	"425 25"
		layout	LAYOUT_LEFT_RIGHT_FLOW
	}
	data id {
		integer	-1
	}
	string key {
		size	"150 25"
	}
	string description {
		size	"250 25"
	}
}

component cmpKeyList extends panel {
	pos		"50 150"
	size	"450 530"
	layout	LAYOUT_TOP_DOWN_FLOW
	wheelscrollable	true
	onViewChange {
		*node:this.parent.keylist_scroll@fullsize = <fullsize>
		*node:this.parent.keylist_scroll@current = <viewpos>
		*node:this.parent.keylist_scroll@viewsize = <viewsize>
	}
}

window options
{
	{
		noticepos	"512 110"
	}
	// ===========
	// BACKGROUND
	// ===========
	image background
	{
		src		"background/options_bg"
	}
	// ============
	// HIDE PANELS
	// ============
	func hidepanels
	{
		*node:root.options_overall@invis = true
		*node:root.options_video@invis = true
		*node:root.options_gameplay@invis = true
		*node:root.options_keys@invis = true
		*node:root.options_sound@invis = true
	}
	// =================================================
	// MAIN OPTIONS MENU (OVERALL/VIDEO/SOUND/GAMEPLAY)
	// =================================================
	cmpMenuButton button_overall
	{
		tooltip		"_Overall game settings"
		string		"_Overall"
		pos			"80 170"
		onClick
		{
			call *node:root.hidepanels
			*node:root.options_overall@invis = false
		}
	}
	cmpMenuButton button_video
	{
		tooltip		"_Video settings"
		string		"_Video"
		pos			"80 240"
		onClick
		{
			call *node:root.hidepanels
			*node:root.options_video@invis = false
		}
	}
	cmpMenuButton button_sound
	{
		tooltip		"_Sound settings"
		string		"_Audio"
		pos			"80 310"
		onClick
		{
			call *node:root.hidepanels
			*node:root.options_sound@invis = false
		}
	}
	cmpMenuButton button_gameplay
	{
		tooltip		"_Battlescape camera, gameplay and input settings"
		string		"_Gameplay"
		pos			"80 380"
		onClick
		{
			call *node:root.hidepanels
			*node:root.options_gameplay@invis = false
		}
	}
	cmpMenuButton button_back
	{
		tooltip		"_Leave the options menu"
		string		"_Back"
		pos			"80 470"
		onClick		{ cmd "ui_pop;" }
	}

	// ===============================
	// MAIN OVERALL GAME OPTIONS MENU
	// ===============================
	panel options_overall
	{
		{
			pos			"415 0"
			size		"608 768"
		}

		cmpMenuPageHeader overallsettings
		{
			string		"_Overall Game Settings"
		}
		// ======================
		// PLAYER'S NAME SUBMENU
		// ======================
		string settings_name
		{
			string 		"_Name"
			pos			"62 110"
			size		"200 20"
			color		"1.0 0.75 0 1"
		}
		// =====================
		// CHANGE PLAYER'S NAME
		// =====================
		textentry slot_name
		{
			string		*cvar:cl_name
			background		"ui/button_green_verysmall"
			color 		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			contentalign	ALIGN_CL
			padding		8
			font		"f_verysmall"
			pos			"72 135"
			size		"192 30"
		}
		// =================
		// LANGUAGE SUBMENU
		// =================
		string settings_language
		{
			string		"_Language Settings"
			pos			"287 110"
			size		"200 20"
			color		"1.0 0.75 0 1"
		}
		optionlist select_language
		{
			tooltip		"_Available languages"
			dataid		OPTION_LANGUAGES
			font		"f_language"
			pos			"297 135"
			cvar		*cvar:s_language
			background		"ui/panel_green_slim"
			size		"185 250"
			color		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			padding		6
			contentalign	ALIGN_CC
			onViewChange	{
				*node:root.options_overall.select_language_scroll@current = <viewpos>
				*node:root.options_overall.select_language_scroll@fullsize = <fullsize>
				*node:root.options_overall.select_language_scroll@viewsize = <viewsize>
			}
		}
		vscrollbar select_language_scroll {
			image		"ui/scrollbar_v_green"
			pos			"485 135"
			height		"250"
			current		0
			viewsize	8
			fullsize	22
			onChange		{
				*node:root.options_overall.select_language@viewpos = <current>
			}
		}
		// =================
		// MODS SUBMENU
		// =================
		string settings_mods
		{
			string		"_Mods"
			pos			"62 170"
			size		"200 20"
			color		"1.0 0.75 0 1"
		}
		optionlist select_mods
		{
			tooltip		"_Available game modifications"
			pos			"72 195"
			cvar		*cvar:fs_gamedir
			background	"ui/panel_green_slim"
			size		"175 185"
			color		"0 .78 0 1"
			selectcolor	"1 1 1 1"
			padding		6
			contentalign	ALIGN_CC
			onViewChange	{
				*node:root.options_overall.select_mods_scroll@current = <viewpos>
				*node:root.options_overall.select_mods_scroll@fullsize = <fullsize>
				*node:root.options_overall.select_mods_scroll@viewsize = <viewsize>
			}
		}
		vscrollbar select_mods_scroll {
			image		"ui/scrollbar_v_green"
			pos			"248 195"
			height		"185"
			current		0
			viewsize	8
			fullsize	22
			onChange		{
				*node:root.options_overall.select_mods@viewpos = <current>
			}
		}
		cmpPopupButton mod_load
		{
			pos			"72 385"
			string		"_Activate mod"
			font		"f_small"
			size		"200 36"
			onClick		{ cmd "fs_restart *cvar:fs_gamedir;" }
		}

		// =====================
		// SEVERAL TIPS SUBMENU
		// =====================
		string settings_tips
		{
			string 		"_Several Tips"
			pos			"62 445"
			size		"320 20"
			color		"1.0 0.75 0 1"
		}
		// =================================
		// MOUSEOVER CURSOR TOOLTIPS (MENU)
		// =================================
		string tooltip_str
		{
			tooltip		"_Show cursor tooltips in menus."
			string		"_Tooltips (menu):"
			pos			"72 470"
			size		"280 20"
		}
		checkbox_blue tooltip
		{
			current		*cvar:mn_show_tooltips
			pos			"485 470"
			size		"20 18"
		}

		// ========================================
		// MOUSEOVER CURSOR TOOLTIPS (BATTLESCAPE)
		// ========================================
		string c_tooltip_str
		{
			tooltip		"_Show cursor tooltips in tactical mode."
			string		"_Tooltips (battlescape):"
			pos			"72 495"
			size		"280 20"
		}
		checkbox_blue c_tooltip
		{
			current		*cvar:cl_show_cursor_tooltips
			pos			"485 495"
			size		"20 18"
		}
		// ===============
		// TIP OF THE DAY
		// ===============
		string tipoftheday_string
		{
			string		"_Show 'Tip of the Day':"
			pos			"72 520"
			size		"280 20"
			font		"f_small"
		}
		checkbox_blue tipoftheday
		{
			current		*cvar:cl_showTipOfTheDay
			pos			"485 520"
			size		"20 18"
		}
		// =====================
		// MENU OPTIONS SUBMENU
		// =====================
		string settings_menu
		{
			string 		"_Menu Options"
			pos			"62 575"
			size		"256 20"
			color		"1.0 0.75 0 1"
		}
		// ================
		// PRECACHE MODELS
		// ================
		string precache_str
		{
			string		"_Precache Models:"
			pos			"72 600"
			size		"320 20"
		}
		checkbox_blue precache
		{
			tooltip		"_Precaching of models means having faster menus."
			current		*cvar:cl_precache
			pos			"485 600"
			size		"20 18"
		}
	}
	// ========================
	// MAIN VIDEO OPTIONS MENU
	// ========================
	panel options_video
	{
		{
			pos			"415 0"
			size		"608 768"
		}

		cmpMenuPageHeader videosettings
		{
			string		"_Video Settings"
		}

		tab video_tabs {
			{
				pos		"47 100"
				size	"483 39"
				cvar	*cvar:mn_videotab
				onChange {
					if ( *cvar:mn_videotab eq "general" ) {
						*node:root.options_video.options_general_video@invis = false
						*node:root.options_video.options_advanced_video@invis = true
						*node:root.options_video.options_other_video@invis = true
					} elif ( *cvar:mn_videotab eq "advanced" ) {
						*node:root.options_video.options_general_video@invis = true
						*node:root.options_video.options_advanced_video@invis = false
						*node:root.options_video.options_other_video@invis = true
					} else {
						*node:root.options_video.options_general_video@invis = true
						*node:root.options_video.options_advanced_video@invis = true
						*node:root.options_video.options_other_video@invis = false
					}
				}
			}
			option general {
				label	"_General"
				value	"general"
			}
			option advanced {
				label	"_Advanced"
				value	"advanced"
			}
			option other {
				label	"_Other"
				value	"other"
			}
		}

		// ================================
		// GENERAL VIDEO OPTIONS TAB PANEL
		// ================================
		panel options_general_video
		{
			{
				pos			"45 145"
				size		"498 680"
			}
			// ============================
			// RESOLUTION SETTINGS SUBMENU
			// ============================
			string settings_resolution
			{
				string 		"_Resolution and Display Settings"
				pos			"22 10"
				size		"320 20"
				color		"1.0 0.75 0 1"
			}

			// ===========
			// RESOLUTION
			// ===========
			string res_str
			{
				tooltip		"_The resolution (x*y) in pixel which UFO:AI should use."
				string		"_Resolution:"
				pos			"32 35"
				size		"240 20"
			}
			// ===============================
			// SELECT SCREEN RESOLUTION RATIO
			// ===============================
			selectbox select_ratio
			{
				{
					tooltip		"_Display only resolutions using this screen ratio."
					cvar		*cvar:mn_vidratio
					pos			"215 37"
					size		"121 20"
					onChange {	cmd "cl_setratiofilter <cvar:mn_vidratio>"	}
				}
				option all
				{
					label	"_All ratios"
					value	"all"
				}
				option r5_4
				{
					label	"5:4"
					value	"1.25"
				}
				option r4_3
				{
					label	"4:3"
					value	"1.3333333"
				}
				option r3_2
				{
					label	"3:2"
					value	"1.5"
				}
				option r16_10
				{
					label	"16:10"
					value	"1.6"
				}
				option r16_9
				{
					label	"16:9"
					value	"1.7777777"
				}
				option custom
				{
					label	"_Others"
					value	"custom"
				}
			}
			// =========================
			// SELECT SCREEN RESOLUTION
			// =========================
			selectbox select_res
			{
				tooltip		"_Select your desired screen resolution."
				cvar		*cvar:vid_mode
				pos			"340 37"
				size		"125 20"
				dataid		OPTION_VIDEO_RESOLUTIONS
			}
			// ===========
			// FULLSCREEN
			// ===========
			string fullscreen_str
			{
				string		"_Fullscreen:"
				pos			"32 60"
				size		"320 20"
			}
			checkbox_blue fullscreen
			{
				tooltip		"_Play UFO:AI in fullscreen."
				current		*cvar:vid_fullscreen
				pos			"445 60"
				size		"20 18"
			}
			// ===============
			// STRETCH SCREEN
			// ===============
			string stretch_str
			{
				string		"_Stretch the Screen with a 4:3 Ratio:"
				pos			"32 85"
				size		"370 20"
			}
			checkbox_blue stretch
			{
				current		*cvar:vid_stretch
				pos			"445 85"
				size		"20 18"
			}
			// =============================================
			// INFORM THE PLAYER ABOUT CTRL+G MOUSEGRABBING
			// =============================================
			string mousegrabbing_info_str
			{
				tooltip		"_Prevent the mousepointer from leaving the game window."
				string		"_Hint: Press CTRL+G to grab/release the mousepointer."
				pos			"32 110"
				size		"410 20"
				visiblewhen	"*cvar:vid_fullscreen == 0"
				font		"f_verysmall"
			}
			// =====================================
			// MONITOR CALIBRATION CONTROLS SUBMENU
			// =====================================
			string settings_calibration
			{
				tooltip		"_Calibrate your display here."
				string 		"_Monitor Calibration Controls"
				pos			"22 165"
				size		"320 20"
				color		"1.0 0.75 0 1"
			}
			// ======
			// GAMMA
			// ======
			string gamma_str
			{
				string		"_Gamma:"
				pos			"32 190"
				size		"170 20"
			}
			image gamma_rahmen
			{
				src		"ui/bar_rahmen"
				pos			"208 192"
			}
			bar gamma_bar
			{
				current		*cvar:vid_gamma
				pos			"217 197"
				size		"238 6"
				color		"0 0.8 0 1"
				max			3.0
				onClick		{ }
			}
			// ============================
			// SCREENSHOT SETTINGS SUBMENU
			// ============================
			string settings_screenshot
			{
				tooltip		"_The default key to make screenshots is F12."
				string 		"_Screenshot Settings"
				pos			"22 320"
				size		"256 20"
				color		"1.0 0.75 0 1"
			}
			// ==================
			// SCREENSHOT FORMAT
			// ==================
			string screenshot_format_str
			{
				tooltip		"_The file format used for compressing and saving your screenshots."
				string		"_Screenshot Format:"
				pos			"32 345"
				size		"320 20"
			}
			selectbox screenshot_format
			{
				{
					cvar		*cvar:r_screenshot_format
					pos			"340 345"
					size		"125 20"

					onChange {
						call *node:root.options_video.options_general_video.show_jpeg_options
					}
				}
				option screenshot_format_jpeg
				{
					label	"jpg"
					value	"jpg"
				}
				option screenshot_format_png
				{
					label	"png"
					value	"png"
				}
				option screenshot_format_tga
				{
					label	"tga"
					value	"tga"
				}
			}
			// ========================
			// JPEG SCREENSHOT QUALITY
			// ========================
			func show_jpeg_options {
				if ( *cvar:r_screenshot_format eq "jpg" ) {
					*node:parent.jpeg_options@invis = false
				} else {
					*node:parent.jpeg_options@invis = true
				}
			}
			panel jpeg_options {
				{
					pos		"30 370"
					size	"440 25"
				}
				string jpeg_quality_str {
					string	"_jpeg Quality:"
					pos		"0 0"
					size	"170 20"
				}
				image jpeg_quality_rahmen {
					src		"ui/bar_rahmen"
					pos		"178 2"
				}
				bar jpeg_quality_bar {
					current	*cvar:r_screenshot_jpeg_quality
					pos		"187 7"
					size	"180 6"
					color	"0 0.8 0 1"
					max		100
				}
			}
			// ==============================
			// OTHER DISPLAY OPTIONS SUBMENU
			// ==============================
			string settings_display_other
			{
				string 		"_Other Display Settings"
				pos			"22 440"
				size		"320 20"
				color		"1.0 0.75 0 1"
			}

			// ============================
			// SHOW FRAMES/SECOND RENDERED
			// ============================
			string fps_str
			{
				string		"_Show FPS:"
				pos			"32 465"
				size		"320 20"
			}
			checkbox_blue fps
			{
				tooltip		"_Display the number of rendered frames per second."
				current		*cvar:cl_fps
				pos			"445 465"
				size		"20 18"
			}

			// ================
			// SHOW INTROVIDEO
			// ================
			string intro_video
			{
				tooltip		"_Don't play the video when loading the game."
				string		"_Skip Intro Video:"
				pos			"32 490"
				size		"320 20"
			}
			checkbox_blue intro
			{
				tooltip		"_Default is to play the intro video just once at the first game start."
				current		*cvar:cl_introshown
				pos			"445 490"
				size		"20 18"
			}
		}

		// =================================
		// ADVANCED VIDEO OPTIONS TAB PANEL
		// =================================
		panel options_advanced_video
		{
			{
				pos			"45 145"
				size		"498 680"
			}

			// =========================
			// TEXTURE SETTINGS SUBMENU
			// =========================
			string settins_h_texture
			{
				size		"320 20"
				color		"1.0 0.75 0 1"
				string 		"_Texture Settings"
				pos			"22 10"
			}
			// =======================
			// TEXTURE RESOLUTION CAP
			// =======================
			string texres_str
			{
				tooltip		"_Sets the maximum size for textures and caps resolutions higher than that value."
				string		"_Resolution Cap:"
				pos			"32 35"
				size		"250 20"
			}
			selectbox texres_box
			{
				{
					tooltip		"_Low means a maximum of 256x256 pixel texture resolution. Extreme means up to 8192x8192 pixel."
					cvar		*cvar:r_maxtexres
					pos			"340 37"
					size		"125 20"
				}
				option low_value {
					label	"_Low"
					value	"256"
				}
				option medium_value {
					label	"_Medium"
					value	"512"
				}
				option normal_value {
					label	"_Normal"
					value	"1024"
				}
				option high_value {
					label	"_High"
					value	"2048"
				}
				option excellent_value {
					label	"_Excellent"
					value	"4096"
				}
				option extrem_value {
					label	"_Extreme"
					value	"8192"
				}
			}

			// =======================
			// TEXTURE FILTERING
			// =======================
			string texmode_str
			{
				tooltip		"_Sets the texture filtering and mipmapping method."
				string		"_Filtering:"
				pos			"32 60"
				size		"150 20"
			}
			selectbox texmode_box
			{
				{
					cvar		*cvar:r_texturemode
					pos			"169 62"
					size		"296 20"
				}

				option low_value {
					label	"_No filtering, no mipmaps"
					value	"GL_NEAREST"
				}
				option medium_value {
					label	"_Bilinear filtering, no mipmaps"
					value	"GL_LINEAR"
				}
				option normal_value {
					label	"_No filtering, mipmaps"
					value	"GL_NEAREST_MIPMAP_NEAREST"
				}
				option high_value {
					label	"_Bilinear filtering, mipmaps"
					value	"GL_LINEAR_MIPMAP_NEAREST"
				}
				option excellent_value {
					label	"_Trilinear filtering, mipmaps"
					value	"GL_NEAREST_MIPMAP_LINEAR"
				}
				option extrem_value {
					label	"_Bilinear filtering, trilinear filtering, mipmaps"
					value	"GL_LINEAR_MIPMAP_LINEAR"
				}
			}

			// ====================
			// TEXTURE COMPRESSION
			// ====================
			string compression_str
			{
				tooltip		"_Enable texture compression for better memory performance, disable for better rendering quality."
				string		"_Compression:"
				pos			"32 85"
				size		"320 20"
			}
			checkbox_blue compression
			{
				current		*cvar:r_ext_texture_compression
				pos			"445 85"
				size		"20 18"
			}
			// ============
			// TEXTURE LOD
			// ============
			string lod_str
			{
				tooltip		"_Texture Level-of-Detail. Enabling this might result in some blurry textures if you zoom out a little bit."
				string		"_LoD:"
				pos			"32 110"
				size		"250 20"
			}
			checkbox_blue lod
			{
				current		*cvar:r_texture_lod
				pos			"445 110"
				size		"20 18"
			}
			// ======================
			// ANISOTROPIC FILTERING
			// ======================
			string aniso_str
			{
				tooltip		"_The degree or ratio of anisotropic filtering. High ratios will make distant textures appear more sharp. Very high ratios are only recommended when playing in high resolutions."
				string		"_Anisotropy Level:"
				pos			"32 135"
				size		"320 20"
			}
			string aniso_value
			{
				string		*cvar:r_anisotropic
				pos			"420 135"
				size		"32 20"
			}
			/** @todo update max with GL max anisotropic */
			spinner_blue aniso_change
			{
				tooltip		"_Very high values are only recommended when playing in very high screen resolutions."
				current		*cvar:r_anisotropic
				pos			"448 135"
				min			0
				max			16
				delta		1
			}

			// ========================
			// OPENGL SETTINGS SUBMENU
			// ========================
			string settings_opengl
			{
				string 		"_OpenGL Settings"
				pos			"22 190"
				size		"256 20"
				color		"1.0 0.75 0 1"
			}
			// =============
			// GLSL SHADERS
			// =============
			string glsl_str
			{
				tooltip		"_OpenGL GLSL Shaders are available only if your graphics-card driver fully supports OpenGL 2.0+ and will enhance UFO:AI using advanced shading techniques."
				string		"_GLSL Shaders:"
				pos			"32 215"
				size		"240 20"
			}
			selectbox glsl_ver
			{
				{
					tooltip	"_Set the OpenGL shader quality - or disable them."
					cvar	*cvar:r_programs
					pos		"300 215"
					size	"165 20"
					onChange {
						call *node:root.options_video.options_advanced_video.show_opengl_options
					}
				}
				option disabled_value
				{
					label   "_Disabled"
					value   "0"
				}
				option low_value
				{
					label   "_Low quality"
					value   "1"
				}
				option medium_value
				{
					label   "_Medium quality"
					value   "2"
				}
				option high_value
				{
					label   "_High quality"
					value   "3"
				}
			}
			// we only show OpenGL options if shaders are enabled
			func show_opengl_options {
				if ( *cvar:r_programs == "0" ) {
					*node:parent.opengl_options@invis = true
				} else {
					*node:parent.opengl_options@invis = false
				}
			}
			panel opengl_options {
				{
					pos		"0 0"
					size	"608 768"
					ghost	true
				}
				// ===============
				// VERTEX BUFFERS
				// ===============
				string vbo_str
				{
					tooltip		"_Vertex Buffers are submitting extensible arrays of vertex attributes to be mapped into the GPU for fast drawing."
					string		"_Vertex Buffers:"
					pos			"32 240"
					size		"240 20"
				}
				checkbox_blue vbo
				{
					current		*cvar:r_vertexbuffers
					pos			"445 240"
					size		"20 18"
				}
				// ==================================================================
				// MULTISAMPLE BUFFERS AND MULTISAMPLE BUFFERS SWAP CONTROL (V-SYNC)
				// ==================================================================
				string samplebuf_swapcontrol_str
				{
					tooltip		"_OpenGL multisample buffers provide a mechanism to antialias all GL primitives. / The Swap Interval selects when a buffer swap will occur. The value '0' means that the swap will occur immediately (also known as 'v-sync off')."
					string		"_Multisample Buffers / Swap Interval:"
					pos			"32 265"
					size		"320 20"
				}
				string samplebuf_value
				{
					tooltip		"_Choose between 0-4 Multisample Buffers. Higher values will provide better antialiasing and will enhance picture quality."
					string		*cvar:r_multisample
					pos			"360 265"
					size		"24 20"
				}
				spinner_blue samplebuf_change
				{
					current		*cvar:r_multisample
					pos			"388 265"
					min			0
					max			4
					delta		1
				}
				string swapcontrol_value
				{
					tooltip		"_Set to '0' to turn v-sync off (faster). Set to '1' or higher to turn v-sync on (better quality)."
					string		*cvar:r_swapinterval
					pos			"420 265"
					size		"20 20"
				}
				spinner_blue swapinterval_change
				{
					current		*cvar:r_swapinterval
					pos			"448 265"
					min			0
					max			2
					delta		1
				}
				// ==============================
				// POSTPROCESSING SHADER EFFECTS
				// ==============================
				string postprocess_str
				{
					tooltip		"_Post-processing shader effects like glowing textures."
					string		"_Post-Processing Shader Effects:"
					pos			"32 290"
					size		"370 20"
				}
				checkbox_blue postprocess
				{
					tooltip		"_Enable post processing shader effects."
					current		*cvar:r_postprocess
					pos			"445 290"
					size		"20 18"
				}
				// =======================================================
				// REALTIME LIGHTING AND MAXIMUM NUMBER OF DYNAMIC LIGHTS
				// =======================================================
				string light_dynamiclights_str
				{
					tooltip		"_Realtime lighting of the 3D world in UFO:AI. / Maximum number of dynamic hardware accelerated lights allowed."
					string		"_Realtime Lighting / Max Dynamic Lights:"
					pos			"32 395"
					size		"320 20"
				}
				string dynamiclights_value
				{
					string		*cvar:r_dynamic_lights
					pos			"420 395"
					size		"100 20"
				}
				spinner_blue dynamiclights_change
				{
					tooltip		"_Change the number of allowed hardware accelerated dynamic lights."
					current		*cvar:r_dynamic_lights
					pos			"448 395"
					min			0
					max			7
					delta		1
					onChange {
						call *node:root.options_video.options_advanced_video.opengl_options.show_lighting_options
					}
				}
				// we only show lighting options if realtime lighting is enabled
				func show_lighting_options {
					if ( *cvar:r_dynamic_lights == "0" ) {
						*node:parent.lighting_options@invis = true
					} else {
						*node:parent.lighting_options@invis = false
					}
				}
				panel lighting_options {
					{
						pos		"0 0"
						size	"608 768"
						ghost	true
					}
					// ====================
					// NORMAL-/BUMPMAPPING
					// ====================
					string bumpmapping_str
					{
						tooltip		"_Also known as bumpmapping."
						string		"_Texture Normalmapping:"
						pos			"32 420"
						size		"320 20"
					}
					checkbox_blue bumpmapping
					{
						tooltip		"_Enable normal-/bumpmapping."
						current		*cvar:r_bumpmap
						pos			"445 420"
						size		"20 18"
					}
				}
				// ========================
				// GEOSCAPE 3DMAP LIGHTING
				// ========================
				string m3dmapambient_str
				{
					tooltip		"_Amount of light emitting from the sun and shining on the earth."
					string		"_Geo Ambient Lighting:"
					pos			"32 445"
					size		"320 20"
				}
				image m3dmapambient_rahmen
				{
					src		"ui/bar_rahmen"
					pos			"204 472"
				}
				bar m3dmapambient_bar
				{
					current		*cvar:cl_3dmapAmbient
					pos			"213 477"
					size		"238 6"
					color		"0 .8 0 1"
					min			-1
					max			3
					onClick		{ }
				}
				// ========
				// SHADOWS
				// ========
				/*
				string shadows_str
				{
					tooltip		"_At the moment these are just drop-shadows."
					string		"_Shadows:"
					pos			"32 370"
					size		"240 20"
				}
				checkbox_blue shadows
				{
					tooltip		"_Enable shadows."
					current		*cvar:r_shadows
					pos			"445 370"
					size		"20 18"
				}
				*/
			}
			// ==================================
			// LIGHT AND SHADOW SETTINGS SUBMENU
			// ==================================
			string settings_light_shadow
			{
				string 		"_Light and Shadow"
				pos			"22 345"
				size		"256 20"
				color		"1.0 0.75 0 1"
			}
			// ===============================
			// LIGHTMAP RESOLUTION BLOCK SIZE
			// ===============================
			string lightmapres_str
			{
				tooltip		"_The lightmap block size defines the resolution of the lightmap. Higher block size means using more detailed lightmaps."
				string		"_Lightmap Block Size:"
				pos			"32 370"
				size		"240 20"
			}
			selectbox lightmapres_box
			{
				{
					tooltip		"_Low means a maximum lightmap resolution of 512x512 pixel. High means up to 2048x2048 pixel."
					cvar		*cvar:r_maxlightmap
					pos			"339 372"
					size		"125 20"
				}
				option low_value {
					label	"_Low"
					value	"256"
				}
				option medium_value {
					label	"_Medium"
					value	"512"
				}
				option normal_value {
					label	"_Normal"
					value	"1024"
				}
				option high_value {
					label	"_High"
					value	"2048"
				}
			}
			// we only show the option to disable the intel hack if we are using the intel driver
			func show_intel_hack_option {
				if ( *cvar:r_vendor_non_intel == "1" ) {
					*node:parent.intel_hack_option@invis = true
				} else {
					*node:parent.intel_hack_option@invis = false
				}
			}
			panel intel_hack_option {
				{
					pos		"0 0"
					size	"608 768"
					ghost	true
				}
				// ====================
				// RENDERER INTEL HACK
				// ====================
				string intelhack_str
				{
					tooltip		"_Enabled by default for compatibility reasons."
					string		"_Intel Graphics Hardware Hack:"
					pos			"32 535"
					size		"320 20"
				}
				checkbox_blue intelhack
				{
					tooltip		"_This hack deactivates GLSL shaders on Intel chips. Disable it to be able to turn on advanced rendering features."
					current		*cvar:r_intel_hack
					pos			"445 535"
					size		"20 18"
				}
			}
		}

		// ================
		// OTHER TAB PANEL
		// ================
		panel options_other_video
		{
			{
				pos			"45 145"
				size		"498 680"
			}

			// =======================
			// OTHER SETTINGS SUBMENU
			// =======================
			string settings_othergfx
			{
				string 		"_Other"
				pos			"22 10"
				size		"320 20"
				color		"1.0 0.75 0 1"
			}
			// =======
			// FLARES
			// =======
			string flares_str
			{
				tooltip		"_Flares are used to display light reflection effects. Flare visibility is calculated according to the angle of the surface emitting the light."
				string		"_Flares:"
				pos			"32 35"
				size		"240 20"
			}
			checkbox_blue flares
			{
				current		*cvar:r_flares
				pos			"445 35"
				size		"20 18"
			}
			// ========
			// CORONAS
			// ========
			string coronas_str
			{
				tooltip		"_Coronas are soft, alpha-blended, rounded polygons."
				string		"_Coronas:"
				pos			"32 60"
				size		"240 20"
			}
			checkbox_blue coronas
			{
				current		*cvar:r_coronas
				pos			"445 60"
				size		"20 18"
			}
			// ====
			// FOG
			// ====
			string fog_str
			{
				tooltip		"_Fog does not relate to any weather effect here, but when enabled distant textures on the battlescape are faded out."
				string		"_Fog:"
				pos			"32 85"
				size		"240 20"
			}
			checkbox_blue fog
			{
				current		*cvar:r_fog
				pos			"445 85"
				size		"20 18"
			}
			// =========================
			// WEATHER PARTICLE EFFECTS
			// =========================
			string ptlweather_str
			{
				tooltip		"_Weather effects like rain or snow."
				string		"_Weather Effects:"
				pos			"32 110"
				size		"240 20"
			}
			checkbox_blue ptlweather
			{
				current		*cvar:cl_particleweather
				pos			"445 110"
				size		"20 18"
			}
			// ================
			// MATERIAL SYSTEM
			// ================
			string materialsys_str
			{
				tooltip		"_The material system is used to display texture animations, effects like rotating or stretching or scrolling textures and manipulate rendering through applying special material properties like specularity or hardness to defined textures."
				string		"_Material System:"
				pos			"32 135"
				size		"240 20"
			}
			checkbox_blue materialsys
			{
				current		*cvar:r_materials
				pos			"445 135"
				size		"20 18"
			}

			button video_low
			{
				tooltip	"_Switch to low video settings"
				pos		"32 200"
				size	"120 40"
				string	"_Low"
				background	"ui/button_green_small"
				color	"0 0.5 0 1"
				selectcolor "1 1 1 1"
				onClick
				{
					cmd "exec video-low.cfg;"
				}
			}
			button video_medium
			{
				tooltip	"_Switch to medium video settings"
				pos		"162 200"
				size	"120 40"
				string	"_Medium"
				background	"ui/button_green_small"
				color	"0 0.5 0 1"
				selectcolor "1 1 1 1"
				onClick
				{
					cmd "exec video-medium.cfg;"
				}
			}
			button video_high
			{
				tooltip	"_Switch to very high video settings"
				pos		"292 200"
				size	"120 40"
				string	"_High"
				background	"ui/button_green_small"
				color	"0 0.5 0 1"
				selectcolor "1 1 1 1"
				onClick
				{
					cmd "exec video-high.cfg;"
				}
			}
		}
	}

	// ========================
	// MAIN SOUND OPTIONS MENU
	// ========================
	panel options_sound
	{
		{
			pos			"415 0"
			size		"608 768"
		}
		// ===============
		// AUDIO SETTINGS
		// ===============
		cmpMenuPageHeader soundsettings
		{
			string		"_Audio Settings"
		}
		// =======================
		// VOLUME CONTROL SUBMENU
		// =======================
		string settings_volume
		{
			string		"_Volume Control"
			pos			"62 110"
			size		"320 20"
			color		"1.0 0.75 0 1"
		}
		// =====================
		// ENABLE/DISABLE AUDIO
		// =====================
		string sound_init_str
		{
			tooltip		"_Enable or disable sound."
			string		"_Enable Audio:"
			pos			"72 135"
			size		"240 20"
		}
		checkbox_blue sound_init
		{
			current		*cvar:snd_init
			pos			"485 135"
			size		"20 18"
			onChange {
				call *node:root.options_sound.show_advanced_sound_options
			}
		}
		// we only show advanced_sound_options if audio is enabled
		func show_advanced_sound_options {
			if ( *cvar:snd_init == 1 ) {
				*node:parent.advanced_sound_options@invis = false
			} else {
				*node:parent.advanced_sound_options@invis = true
			}
		}
		panel advanced_sound_options {
			{
				pos		"0 0"
				size	"608 768"
				ghost	true
			}
			// =============
			// MUSIC VOLUME
			// =============
			string music_str
			{
				string		"_Music:"
				pos			"72 160"
				size		"150 20"
			}
			image music_rahmen
			{
				src		"ui/bar_rahmen"
				pos			"248 162"
			}
			bar music_bar
			{
				current		*cvar:snd_music_volume
				pos			"257 167"
				size		"238 6"
				color		"0 .8 0 1"
				max			128
				onClick		{ }
			}

			// =====================
			// SOUND EFFECTS VOLUME
			// =====================
			string effects_str
			{
				string		"_Sound Effects:"
				pos			"72 185"
				size		"170 20"
			}
			image effects_rahmen
			{
				src		"ui/bar_rahmen"
				pos			"248 187"
			}
			bar effects_bar
			{
				current		*cvar:snd_volume
				pos			"257 192"
				size		"238 6"
				color		"0 .8 0 1"
				max			1.0
				onClick		{ }
			}
			// ============
			// MENU SOUNDS
			// ============
			// on/off only at the moment
			//
			string menusounds_str
			{
				string		"_Menu Sounds:"
				pos			"72 210"
				size		"170 20"
			}
			checkbox_blue menu_sounds_checkbox
			{
				tooltip		"_Turn the menu sounds on or off."
				current		*cvar:ui_sounds
				pos			"485 210"
				size		"20 18"
			}
			// ============================
			// OTHER AUDIO OPTIONS SUBMENU
			// ============================
			string settings_sound_other
			{
				string		"_Advanced Audio Options"
				pos			"62 265"
				size		"240 20"
				color		"1.0 0.75 0 1"
			}
			// ==================
			// AUDIO SAMPLE RATE
			// ==================
			string rate_str
			{
				tooltip		"_The sample rate frequency in kHz. Use higher values for better sound quality."
				string		"_Audio Sample Rate:"
				pos			"72 290"
				size		"240 20"
			}
			selectbox rate_box
			{
				{
					cvar		*cvar:snd_rate
					pos			"359 295"
					size		"125 20"
				}
				option low_value
				{
					label	"11025"
					value	"11025"
				}
				option medium_value
				{
					label	"22050"
					value	"22050"
				}
				option normal_value
				{
					label	"44100"
					value	"44100"
				}
				option high_value
				{
					label	"48000"
					value	"48000"
				}
			}
			button rate_test_pic
			{
				tooltip		"_Restart the sound-engine and apply your settings."
				icon		"icons/bordered_magnifying_glass"
				pos			"486 297"
				size		"16 16"
				padding		0
				onClick		{ cmd "snd_restart;" }
			}
		}
	}
	// ========================================
	// MAIN CAMERA/GAMEPLAY/INPUT OPTIONS MENU
	// ========================================
	panel options_gameplay
	{
		{
			pos			"415 0"
			size		"608 768"
		}
		// ==================
		// GAMEPLAY SETTINGS
		// ==================
		cmpMenuPageHeader gameplaysettings_str
		{
			string		"_Gameplay Settings"
		}
		// ====================================
		// BATTLESCAPE CAMERA SETTINGS SUBMENU
		// ====================================
		string settings_camera
		{
			string		"_Battlescape Camera Settings"
			pos			"62 110"
			size		"320 20"
			color		"1.0 0.75 0 1"
		}
		// ================================
		// BATTLESCAPE CAMERA SCROLL SPEED
		// ================================
		string camspeed_str
		{
			string		"_Scroll Speed:"
			pos			"72 135"
			size		"170 20"
		}
		image camspeed_rahmen
		{
			src		"ui/bar_rahmen"
			pos			"248 137"
		}
		bar camspeed_bar
		{
			current		*cvar:cl_cammovespeed
			pos			"257 142"
			size		"238 6"
			color		"0 .8 0 1"
			min			5
			max			3000
			onClick		{ }
		}
		// ============================
		// BATTLESCAPE CAMERA ROTATION
		// ============================
		string camrot_str
		{
			string		"_Rotation Speed:"
			pos			"72 160"
			size		"170 20"
		}
		image camrot_rahmen
		{
			src		"ui/bar_rahmen"
			pos			"248 162"
		}
		bar camrot_bar
		{
			current		*cvar:cl_camrotspeed
			pos			"257 167"
			size		"238 6"
			color		"0 .8 0 1"
			min			5
			max			1000
			onClick		{ }
		}
		// =======================================================
		// BATTLESCAPE CAMERA MOVE DECELERATION (cl_cammoveaccel)
		// =======================================================
		string cammovedecel_str
		{
			tooltip		"_The move deceleration of the battlescape camera. Lower this value if you want smoother camera movement, increase it if you want cameramovement to decelerate faster or stop immediately."
			string		"_Move Deceleration:"
			pos			"72 185"
			size		"170 20"
		}
		image cammovedecel_rahmen
		{
			src		"ui/bar_rahmen"
			pos			"248 187"
		}
		bar cammovedecel_bar
		{
			current		*cvar:cl_cammoveaccel
			pos			"257 192"
			size		"238 6"
			color		"0 .8 0 1"
			min			0
			max			3000
			onClick		{ }
		}
		// ==================================
		// BATTLESCAPE CAMERA ISOMETRIC MODE
		// ==================================
		string isomode_str
		{
			tooltip		"_Isometric Mode means the 3D world will be rendered without perspective correction."
			string		"_Isometric Mode:"
			pos			"72 210"
			size		"320 20"
		}
		checkbox_blue isomode
		{
			current		*cvar:r_isometric
			pos			"485 210"
			size		"20 18"
		}
		// ===============================
		// BATTLESCAPE CAMERA CENTER VIEW
		// ===============================
		string centerview_str
		{
			tooltip		"_Center the camera view automatically at the soldier when selecting it, or at the enemy when he is shooting or appearing."
			string		"_Center View:"
			pos			"72 235"
			size		"320 20"
		}
		checkbox_blue centerview_value
		{
			current		*cvar:cl_centerview
			pos			"485 235"
			size		"20 18"
		}
		// ======================================
		// BATTLESCAPE GAMEPLAY SETTINGS SUBMENU
		// ======================================
		string settings_gameplay
		{
			string		"_Battlescape Gameplay Settings"
			pos			"62 290"
			size		"320 20"
			color		"1.0 0.75 0 1"
		}
		// ================
		// BATTLESCAPE HUD
		// ================
		string hud_str
		{
			tooltip		"_Select the Head-Up-Display for the battlescape."
			string		"_HUD Design:"
			pos			"72 315"
			size		"250 20"
		}
		// @note selectbox completed by the confunc add_hud_name
		selectbox select_hud
		{
			cvar		*cvar:cl_hud
			pos			"350 315"
			size		"155 20"
		}
		// ============================
		// BATTLESCAPE CONFIRM ACTIONS
		// ============================
		string confirm_str
		{
			tooltip		"_Enabling this will give you a preview of your actions on the battlescape which you then have to confirm."
			string		"_Confirm Actions:"
			pos			"72 340"
			size		"240 20"
		}
		selectbox select_confirmactions
		{
			{
				tooltip		"_'No' means all your orders on the battlescape will be executed immediately. 'Yes' means you will be prompted to confirm all of your orders before the soldier begins to execute them. 'Only for moving' means the path will be displayed before you confirm a soldier's move."
				cvar		*cvar:confirm_actions
				pos			"350 340"
				size		"155 20"
			}
			option confirm_value {
				label	"_Yes"
				value	"1"
			}
			option noconfirm_value {
				label	"_No"
				value	"0"
			}
			option moveconfirm_value {
				label	"_Moving only"
				value	"2"
			}
		}
		// ======================
		// BATTLESCAPE AUTOSTAND
		// ======================
		string autostand_str
		{
			tooltip		"_Autostand regulates if your soldier should automatically stand up when he is getting the order to move while being crouched."
			string		"_Autostand:"
			pos			"72 365"
			size		"320 20"
		}
		checkbox_blue autostand_value
		{
			current		*cvar:cl_autostand
			pos			"485 365"
			size		"20 18"
		}
		// ============
		// ACTOR SPEED
		// ============
		string actorsspeed_str
		{
			string		"_Actor Speed:"
			pos			"72 390"
			size		"170 20"
		}
		image actorsspeed_rahmen
		{
			src		"ui/bar_rahmen"
			pos			"248 392"
		}
		bar actorsspeed_bar
		{
			tooltip		"_Choose between 0-9, higher values mean higher actor speed."
			current		*cvar:g_actorspeed
			pos			"257 397"
			size		"238 6"
			color		"0 .8 0 1"
			max			9
			onClick		{ }
		}
		// =========================
		// GEOSCAPE OPTIONS SUBMENU
		// =========================
		string settings_geoscape
		{
			string 		"_Geoscape Options"
			pos			"62 445"
			size		"320 20"
			color		"1.0 0.75 0 1"
		}
		// ==================
		// GEOSCAPE 2D/3DMAP
		// ==================
		string m3dmap_str
		{
			tooltip		"_3D or 2D geoscape. To enable this option you have to start a new campaign first."
			string		"_3D Geoscape:"
			pos			"72 470"
			size		"280 20"
		}
		checkbox_blue m3dmap
		{
			current		*cvar:cl_3dmap
			pos			"485 470"
			size		"20 18"
		}
		// ====================
		// GEOSCAPE DECORATION
		// ====================
		string geoscape_deco_str
		{
			tooltip		"_Show/Hide the panel decoration on the geoscape."
			string		"_Show decoration:"
			pos			"72 495"
			size		"280 20"
		}
		checkbox_blue geoscape_deco
		{
			current		*cvar:mn_geoscape_deco
			pos			"485 495"
			size		"20 18"
		}
		// =====================================
		// GEOSCAPE CAMERA MINIMUM/MAXIMUM ZOOM
		// =====================================
		string geocamzoommin_str
		{
			tooltip		"_The minimum/maximum zoom of the geoscape camera"
			string		"_Min/Max Geo-Cam Zoom:"
			pos			"72 520"
			size		"280 20"
		}
		selectbox geocamzoom_min
		{
			{
				tooltip		"_Minimum zoom level of the geoscape camera"
				cvar		*cvar:cl_mapzoommin
				pos			"355 520"
				size		"75 20"
				contentalign	ALIGN_CR
			}
			option geocamzoom_min000
			{
				label	"0.00"
				value	"0"
			}
			option geocamzoom_min020
			{
				label	"0.20"
				value	"0.20"
			}
			option geocamzoom_min040
			{
				label	"0.40"
				value	"0.40"
			}
			option geocamzoom_min060
			{
				label	"0.60"
				value	"0.60"
			}
			option geocamzoom_min080
			{
				label	"0.80"
				value	"0.80"
			}
			// default
			option geocamzoom_min100
			{
				label	"_1.00"
				value	"1.0"
			}
		}
		selectbox geocamzoom_max
		{
			{
				tooltip		"_Maximum zoom level of the geoscape camera"
				cvar		*cvar:cl_mapzoommax
				pos			"430 520"
				size		"75 20"
			}
			option geocamzoom_max20
			{
				label	"_2.0"
				value	"2.0"
			}
			option geocamzoom_max40
			{
				label	"_4.00"
				value	"4.0"
			}
			// default
			option geocamzoom_max60
			{
				label	"_6.00"
				value	"6.0"
			}
			option geocamzoom_max80
			{
				label	"_8.00"
				value	"8.0"
			}
			option geocamzoom_max100
			{
				label	"_10.00"
				value	"10.0"
			}
			option geocamzoom_max120
			{
				label	"_12.00"
				value	"12.0"
			}
		}
		// =================================
		// JOYSTICK/JOYPAD SETTINGS SUBMENU
		// =================================
		string settings_joystick
		{
			string		"_Joystick Settings"
			pos			"62 575"
			size		"320 20"
			color		"1.0 0.75 0 1"
		}
		// ================
		// JOYSTICK/JOYPAD
		// ================
		string joystick_enable_str
		{
			string		"_Enable:"
			pos			"72 600"
			size		"240 20"
		}
		checkbox_blue joystick_enable
		{
			current		*cvar:in_joystick
			pos			"485 600"
			size		"20 18"
			onChange {
				call *node:root.options_gameplay.show_joystick_options
			}
		}
		// we only show joystick_options if the joystick is enabled
		func show_joystick_options {
			if ( *cvar:in_joystick == 1 ) {
				*node:parent.joystick_options@invis = false
			} else {
				*node:parent.joystick_options@invis = true
			}
		}
		panel joystick_options {
			{
				pos		"0 0"
				size	"608 768"
				ghost	true
			}
			// ==========================
			// JOYSTICK/JOYPAD SELECTION
			// ==========================
			string joystick_str
			{
				string		"_Joystick or Joypad:"
				pos			"72 545"
				size		"250 20"
			}
			selectbox select_joystick
			{
				tooltip		"_Available game controllers."
				cvar		*cvar:in_joystickNo
				dataid		OPTION_JOYSTICKS
				pos			"350 545"
				size		"155 20"
			}
			// ===================
			// JOYSTICK THRESHOLD
			// ===================
			string joystick_threshold_str
			{
				tooltip		"_Adjust the joystick threshold to a high enough value, so movement of the analogue stick will be recognized."
				string		"_Threshold:"
				pos			"72 570"
				size		"150 20"
			}
			image joystick_threshold_rahmen
			{
				src		"ui/bar_rahmen"
				pos			"248 572"
			}
			bar joystick_threshold_bar
			{
				current		*cvar:in_joystickThreshold
				pos			"257 577"
				size		"238 6"
				color		"0 .8 0 1"
				min			0.0
				max			1.0
				onClick		{ }
			}
			// ===============
			// JOYSTICK SPEED
			// ===============
			string joystick_speed_str
			{
				string		"_Speed:"
				pos			"72 595"
				size		"150 20"
			}
			image joystick_speed_rahmen
			{
				src		"ui/bar_rahmen"
				pos			"248 597"
			}
			bar joystick_speed_bar
			{
				current		*cvar:in_joystickSpeed
				pos			"257 602"
				size		"238 6"
				color		"0 .8 0 1"
				min			5
				max			30
				onClick		{ }
			}
		}
		// =============
		// KEY BINDINGS
		// =============
		cmpMenuButton button_keys
		{
			string		"_Show Keys"
			pos			"162 635"
			onClick
			{
				*node:root.options_gameplay@invis = true
				*node:root.options_keys@invis = false
				cmd "mn_init_keylist;"
			}
		}
	}

	/**
	 * Add a hud window the user can use to play battlescape
	 * <1> HUD identifier name
	 * <2> HUD label
	 */
	confunc add_hud_name {
		call *node:root.options_gameplay.select_hud@createchild("option<1>", "option")
		*node:root.options_gameplay.select_hud.option<1>@value = <1>
		*node:root.options_gameplay.select_hud.option<1>@label = <2>
	}

	// ==================
	// SHOW KEY BINDINGS
	// ==================
	panel options_keys
	{
		{
			pos			"415 0"
			size		"608 768"
		}

		string showkeys
		{
			string		"_Key Bindings"
			pos			"55 67"
			size		"465 35"
			font		"f_menubig"
			contentalign	ALIGN_CC
			color		"0 0.79 0 1"
		}

		tab nav {
			{
				pos		"47 106"
				size	"480 39"
				cvar	*cvar:ui_keybindingspace
				onChange {
					*node:this.parent.keylist0@invis = true
					*node:this.parent.keylist1@invis = true
					*node:this.parent.keylist2@invis = true
					*node:this.parent.keylist<cvar:ui_keybindingspace>@invis = false
					*node:this.parent.keylist<cvar:ui_keybindingspace>@viewpos = 0
					*node:this.parent.keylist_scroll@current = 0
				}
			}
			option menu {
				label	"_Menu"
				value	"0"
			}
			option global {
				label	"_Global"
				value	"1"
			}
			option battlescape {
				label	"_Battlescape"
				value	"2"
			}
		}
		cmpKeyList keylist0 {
			invis false
		}
		cmpKeyList keylist1 {
			invis true
		}
		cmpKeyList keylist2 {
			invis true
		}
		vscrollbar keylist_scroll {
			image		"ui/scrollbar_v_green"
			pos			"500 150"
			height		"535"
			hidewhenunused true
			onChange {
				*node:this.parent.keylist<cvar:ui_keybindingspace>@viewpos = <current>
			}
		}

		/*
		 * @brief Confunc to add a keybinding entry
		 * <1> id
		 * <2> keyspace menu = 0, global = 1 or battlescape = 2
		 * <3> the key name
		 * <4> description
	     */
		confunc keybinding_add {
			call *node:this.parent.keylist<2>@createChild("key_<1>", "cmpKeyBinding")
			*node:this.parent.keylist<2>.key_<1>.id@integer = <1>
			*node:this.parent.keylist<2>.key_<1>.key@string = "<3>"
			*node:this.parent.keylist<2>.key_<1>.description@string = "<4>"
			*node:this.parent.keylist<2>.key_<1>.description@tooltip = "<4>"
		}
		confunc keybinding_clear {
			call *node:this.parent.keylist0@removeAllChild
			call *node:this.parent.keylist1@removeAllChild
			call *node:this.parent.keylist2@removeAllChild
		}
	}

	game_mod_listener listener {
		confunc mn_mods_result {
			call *node:root.options_overall.select_mods@createchild("option<1>", "option")
			*node:root.options_overall.select_mods.option<1>@value = <1>
			*node:root.options_overall.select_mods.option<1>@label = <1>
		}
	}

	func onWindowOpened
	{
		*cvar:mn_vidratio = "all"
		cmd "cl_setratiofilter <cvar:mn_vidratio>;set ui_keybindingspace 0;"
		call *node:root.hidepanels

		call *node:root.options_overall.select_mods@removeallchild
		cmd "mn_mods;"

		*cvar:mn_videotab = "general"
		*node:root.options_video.options_advanced_video@invis = true
		*node:root.options_video.options_other_video@invis = true
		*node:root.options_video.options_general_video@invis = false

		*node:root.options_overall@invis = false

		// hide unnecessary options
		call *node:root.options_video.options_general_video.show_jpeg_options
		call *node:root.options_video.options_advanced_video.show_opengl_options
		call *node:root.options_video.options_advanced_video.opengl_options.show_lighting_options
		call *node:root.options_video.options_advanced_video.show_intel_hack_option
		call *node:root.options_sound.show_advanced_sound_options
		call *node:root.options_gameplay.show_joystick_options
	}
}
