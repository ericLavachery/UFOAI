// ==================
// TEAM MENU
// ==================

component cmpNamedTbar extends cmpDefNamedTbar {
	{
		size	"440 23"
	}
	image	bdr {
		pos "165 2"
	}
	tbar		bar {
		pos "167 2"
	}
	string	val {
		pos "265 0"
		size "150 20"
	}
}

component cmpNamedBar extends panel {
	{
		size	"440 23"
	}
	image			bdr {
		pos "165 2"
		src "ui/bar_border"
	}
	bar		currbar {
		pos "167 7"
		size "255 6"
		color		".5 .9 .5 1"
		max 100
	}
	bar		initbar {
		pos "167 7"
		size "255 6"
		color		"0 .8 0 1"
		max 100
	}
	string	lbl {
		pos "0 0"
		size "180 20"
		string ""
	}
	string	val {
		pos "265 0"
		size "150 20"
		contentalign ALIGN_UR
	}
}

/**
 * @todo rename the menu according to the file
 */
window team extends iaircraft
{
	// ==================
	// background
	// ==================

	panel actors_background {
		pos		"18 443"
		size	"329 321"
		background	"ui/panel_grey_green_large"
	}

	panel tab_background {
		pos		"24 386"
		size	"986 56"
		background	"ui/panel_tab"
	}
	// ==================
	// aircraft
	// ==================
	model aircraft {
		src			*cvar:mn_aircraft_model
		pos			"478 106"
		size		"529 261"
		origin		"-45 25 0"
		angles		"160 20 -120"
		autoscale	true
	}

	string team_members {
		string	"_Team Members:"
		pos		"480 107"
		size	"200 20"
	}

	string team_hired {
		string		*cvar:mn_hired
		pos			"619 107"
		size		"62 20"
		contentalign	ALIGN_UR
	}

	func listen_close_equipment {
		cmd "aircraft_soldierlist_select <node:root.selected@integer>;"
		cmd "ui_removelistener equipment@onWindowClosed <path:this>;"
	}

	button bt_equipsoldiers {
		string "_Equip soldiers"
		pos		"482 129"
		size	"198 40"
		background	"ui/button_blue"
		color	"1 1 1 0.8"
		selectcolor "1 1 1 1"

		onClick	{
			*cvar:cl_selected2 = <cvar:cl_selected>
			cmd "ui_addlistener equipment@onWindowClosed <path:root>.listen_close_equipment;"
			cmd "ui_push equipment;"
		}
	}
	todo equip_soldiers_subtab {
		pos "463 135"
		string "It would be nice to create a mockup and prototype to equip soldiers with another aircraft tab - should be a very nice short cut"
	}

	// ==================
	// names list
	// ==================

	data selected {
	}

	rows soldierlist_lines {
		pos			"35 460"
		size		"270 283"
		color1		"0 0.08 0 1"
		color2		"0 0.16 0 1"
		lineheight	25
	}
	panel soldierlist {
		pos			"35 460"
		size		"270 283"
		padding		6
		layout		LAYOUT_TOP_DOWN_FLOW
		wheelscrollable	true

		onViewChange {
			*node:parent.soldierlist_scroll@fullsize = <fullsize>
			*node:parent.soldierlist_scroll@current = <viewpos>
			*node:parent.soldierlist_scroll@viewsize = <viewsize>
		}
	}
	vscrollbar soldierlist_scroll {
		image		"ui/scrollbar_v_green"
		pos			"310 462"
		height		"283"
		current		0
		viewsize	8
		fullsize	50

		onChange {
			*node:parent.soldierlist@viewpos = <current>
		}
	}

	// ==================
	// actor info
	// ==================

	panel actor_background {
	{
		pos		"358 443"
		size	"663 321"
		background	"ui/panel_grey_black"
	}

	panel actormodel_background {
		{
			pos		"12 12"
			size	"179 296"
			background	"ui/panel_green"
		}

		image	rank_img {
			pos "8 8"
			size "50 50"
			src *cvar:mn_chrrank_img
		}

		model body
		{
			{
				src		*cvar:mn_body
				skin	*cvar:mn_body_skin
				anim	"stand2"
				angles	"0 70 90"
				scale	"5.5 5.5 5.5"
				pos		"6 6"
				size	"168 283"
				origin	"0 -35 0"
			}

			model head
			{
				src		*cvar:mn_head
				skin	*cvar:mn_head_skin
				tag		"tag_head"
				scale	"1.0 1.0 1.0"
			}

			model right_rweapon
			{
				src		*cvar:mn_rweapon
				tag		"tag_rweapon"
			}

			model right_lweapon
			{
				src		*cvar:mn_lweapon
				tag		"tag_lweapon"
			}
		}

		string	name { pos "8 247" size "165 20" string *cvar:mn_name }
		string	rank_lbl { pos "8 267" size "165 20" string *cvar:mn_chrrank }
	}

	tab actor_stat {
		{
			pos		"209 11"
			size	"438 39"
			cvar	*cvar:mn_actor_stat
			onChange {
				cmd "select_actor_stat <cvar:mn_actor_stat>;"
			}
		}
		option phys {
			label	"_Physical stats"
			value	"stats_phys"
		}
		option missions {
			label	"_Missions info"
			value	"stats_missions"
		}
	}

	panel stats_phys {
		{
			pos			"208 44"
			size		"440 264"
			background	"ui/panel_green_tab"
			layout		LAYOUT_TOP_DOWN_FLOW
			padding		9
		}
		string padding {
			size		"440 16"
		}
		cmpNamedBar pwr {
			bar currbar { current *cvar:mn_vpwr }
			bar initbar { current *cvar:mn_vpwri }
			string lbl  { string "_Strength:" }
			string val  { string *cvar:mn_tpwr }
		}
		cmpNamedBar spd {
			bar currbar { current *cvar:mn_vspd }
			bar initbar { current *cvar:mn_vspdi }
			string lbl  { string "_Speed:" }
			string val  { string *cvar:mn_tspd }
		}
		cmpNamedBar acc {
			bar currbar { current *cvar:mn_vacc }
			bar initbar { current *cvar:mn_vacci }
			string lbl  { string "_Accuracy:" }
			string val  { string *cvar:mn_tacc }
		}
		cmpNamedBar mnd {
			bar currbar { current *cvar:mn_vmnd }
			bar initbar { current *cvar:mn_vmndi }
			string lbl  { string "_Mind:" }
			string val  { string *cvar:mn_tmnd }
		}
		cmpNamedBar cls {
			bar currbar { current *cvar:mn_vcls }
			bar initbar { current *cvar:mn_vclsi }
			string lbl  { string "_Close Combat:" }
			string val  { string *cvar:mn_tcls }
		}
		cmpNamedBar ass {
			bar currbar { current *cvar:mn_vass }
			bar initbar { current *cvar:mn_vassi }
			string lbl  { string "_Assault Weapons:" }
			string val  { string *cvar:mn_tass }
		}
		cmpNamedBar snp {
			bar currbar { current *cvar:mn_vsnp }
			bar initbar { current *cvar:mn_vsnpi }
			string lbl  { string "_Sniper Rifles:" }
			string val  { string *cvar:mn_tsnp }
		}
		cmpNamedBar exp {
			bar currbar { current *cvar:mn_vexp }
			bar initbar { current *cvar:mn_vexpi }
			string lbl  { string "_High-Explosives:" }
			string val  { string *cvar:mn_texp }
		}
		cmpNamedTbar hp {
			tbar bar   {
				current *cvar:mn_vhp
				max *cvar:mn_vhpmax
				image "ui/bar_health"
			}
			string lbl { string "_Health:" }
			string val { string *cvar:mn_thp }
		}
	}

	panel stats_missions {
	{
		pos		"208 44"
		size	"440 264"
		background	"ui/panel_green_tab"
		invis true
	}

	image	rank_img {
		pos "195 30"
		size "50 50"
		src *cvar:mn_chrrank_img
	}

	string	rank_lbl { pos "10 85" size "420 20" string *cvar:mn_chrrank contentalign ALIGN_UC font "f_normal_bold" }

	string	missions_lbl { pos "12 150" size "200 20" string "_Missions performed:" }
	string	missions_val { pos "212 150" size "150 20" string *cvar:mn_chrmis contentalign ALIGN_UR }

	string	kills_lbl { pos "12 170" size "200 20" string "_Enemies killed:" }
	string	kills_val { pos "212 170" size "150 20" string *cvar:mn_chrkillalien contentalign ALIGN_UR }

	string	civ_lbl { pos "12 190" size "200 20" string "_Civilians killed:" }
	string	civ_val { pos "212 190" size "150 20" string *cvar:mn_chrkillcivilian contentalign ALIGN_UR }

	string	team_lbl { pos "12 210" size "200 20" string "_Our team killed:" }
	string	team_val { pos "212 210" size "150 20" string *cvar:mn_chrkillteam contentalign ALIGN_UR }

	}

	}

	/** @todo Activate this again - used for UGVs */
	// ==================
	// additional stuff
	// ==================
	confunc toggle_show_heavybutton {
		//*node:root.bt_switch_list@tooltip	= "_Assign tanks"
		//*node:root.bt_switch_list@icon	= "icons/uvc"
	}
	confunc toggle_show_soldiersbutton {
		//*node:root.bt_switch_list@tooltip	= "_Assign soldiers"
		//*node:root.bt_switch_list@icon	= "icons/soldier"
	}

	/**
	 * @note call when tab actor_stat changed.
	 */
	confunc select_actor_stat {
		if ( <1> eq "stats_phys" ) {
			*node:root.actor_background.stats_phys@invis = false
			*node:root.actor_background.stats_missions@invis = true
		}
		elif ( <1> eq "stats_missions" ) {
			*node:root.actor_background.stats_phys@invis = true
			*node:root.actor_background.stats_missions@invis = false
		}
	}

	/**
	 * @note call when the aircraft status change
	 */
	confunc aircraft_status_change
	{
		if ( *cvar:mn_hired_count > 0 ) {
			*node:root.bt_equipsoldiers@disabled = false
			*node:root.bt_equipsoldiers@tooltip = "_Equip soldiers"
		} else {
			*node:root.bt_equipsoldiers@disabled = true
			*node:root.bt_equipsoldiers@tooltip = "_No soldiers assigned"
		}
	}

	/**
	 * @note iaircraft interface
	 */
	confunc aircraft_change
	{
		cmd "ui_team_fill soldier;"
		if ( *node:root.selected@integer >= 0 ) {
			cmd "aircraft_soldierlist_select <node:root.selected@integer>;"
		}
		*node:root.aircraft_list@lineselected = <1>
		// absent from base, go back to the main GUI
		if ( *cvar:mn_aircraftinbase == 0 ) {
			cmd "ui_pop;ui_push aircraft;"
		}
		cmd "aircraft_status_change;"
	}

	func onWindowOpened
	{
		*node:root.selected@integer = -1
		call *node:root.reset_character_cvars
		cmd "mn_select_aircraft <cvar:mn_aircraft_id>;"
	}
}
