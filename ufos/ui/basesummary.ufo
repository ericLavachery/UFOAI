// ======================
// BASE STATISTICS MENU
// ======================

component cmpBaseSwitcher extends baselayout
{
	size		"67 72"
	color		"1 0 0 0.4"
	bgcolor		"1 1 1 0.4"
	padding		2
	onClick {
		cmd "basesummary_selectbase <baseid>;"
		cmd "mn_base_select <baseid>;"
	}
}

component cmpBSumLine extends panel {
	{
		size	"485 20"
	}
	string main {
		pos		"0 0"
		size	"245 20"
	}
	string sub1 {
		pos		"245 0"
		size	"120 20"
		contentalign ALIGN_CR
	}
	string sub2 {
		pos		"365 0"
		size	"120 20"
		contentalign ALIGN_CR
	}
}

window basesummary
{
	{
		background		"background/base_bg"
	}

	panel basenavigation {
		{
			pos		"621 115"
			size	"338 211"
			background	"ui/panel_grey"
		}
		panel inner_border {
			pos		"19 20"
			size	"298 170"
			background	"ui/panel_grey_black"
			layout		LAYOUT_COLUMN
			layoutColumns	4
			layoutMargin	4
			padding		9
		}
		/**
		 * @brief Clear base switcher
		 */
		confunc bsum_clear_bases {
			call *node:parent.inner_border@removeAllChild
		}
		/**
		 * @brief Add a base to the base switcher
		 * @param <1> base index
		 * @param <2> 1 if this is the actual base 0 otherwise
		 */
		confunc bsum_add_base {
			call *node:parent.inner_border@createchild("base_<1>", "cmpBaseSwitcher")
			*node:parent.inner_border.base_<1>@baseid = <1>
			if ( <2> ) {
				*node:parent.inner_border.base_<1>@bgcolor = "1 1 1 0.7"
			}
		}
	}

	// ===================
	// left side text node
	// ===================

	panel bsum_main {
		{
			pos			"12 103"
			size		"551 655"
			background	"ui/panel_grey_green_large"
		}
		panel bsum_mainarea {
			pos			"23 21"
			size		"485 608"
			layout		LAYOUT_TOP_DOWN_FLOW
			onViewChange	{
				*node:parent.bsum_mainarea_scroll@fullsize = <fullsize>
				*node:parent.bsum_mainarea_scroll@current = <viewpos>
				*node:parent.bsum_mainarea_scroll@viewsize = <viewsize>
			}
		}
		vscrollbar bsum_mainarea_scroll {
			image		"ui/scrollbar_v_green"
			pos			"513 21"
			height		"613"
			current		0
			viewsize	30
			fullsize	30
			hidewhenunused true
			onChange	{ *node:parent.bsum_mainarea@viewpos = <current> }
		}

		/**
		 * @brief clear base summary main panel
		 */
		confunc bsum_main_clearlines {
			call *node:parent.bsum_mainarea@removeAllChild
		}
		/**
		 * @brief add a line to the base summary main panel
		 * @param <1> index
		 * @param <2> 1st, main column
		 * @param <3> 2nd sub column
		 * @param <4> 3th sub column
		 * @param <5> 1 if this is a header line 0 otherwise
		 */
		confunc bsum_main_addline {
			call *node:parent.bsum_mainarea@createchild("bsum_main_<1>", "cmpBSumLine")
			*node:parent.bsum_mainarea.bsum_main_<1>.main@string = <2>
			*node:parent.bsum_mainarea.bsum_main_<1>.sub1@string = <3>
			*node:parent.bsum_mainarea.bsum_main_<1>.sub2@string = <4>
			if ( <5> ) {
				*node:parent.bsum_mainarea.bsum_main_<1>.main@font = "f_small_bold"
				*node:parent.bsum_mainarea.bsum_main_<1>.sub1@font = "f_small_bold"
				*node:parent.bsum_mainarea.bsum_main_<1>.sub2@font = "f_small_bold"
			}
		}
	}

	// ==================
	// info window
	// ==================

	panel info_bg {
		pos	"588 337"
		size	"408 423"
		background	"ui/panel_grey_green_large"
	}

	text txt_info
	{
		pos			"615 362"
		size		"330 370"
		font		"f_small"
		lineheight		16
		tabwidth	50
		rows		"23"
		dataid			TEXT_STANDARD
		onViewChange	{
			*node:root.info_scroll@fullsize = <fullsize>
			*node:root.info_scroll@current = <viewpos>
			*node:root.info_scroll@viewsize = <viewsize>
		}
	}

	vscrollbar info_scroll {
		image		"ui/scrollbar_v_green"
		pos			"950 362"
		height		"370"
		current		0
		viewsize	38
		fullsize	38
		hidewhenunused true
		onChange	{ *node:root.txt_info@viewpos = <current> }
	}

	// ==================
	// Base header
	// ==================

	cmpBaseHeader baseHeader {
	}

	// ==================
	// init
	// ==================
	func onWindowOpened
	{
		cmd "basesummary_selectbase;"
		*cvar:mn_lastsubmenu = "basesummary"
	}
}
