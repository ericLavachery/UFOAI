// ==================
// EQUIPMENT MENU
// ==================

component equipSoldier extends panel {
	{
		size		"260 25"
	}

	data id {
		string		""
	}

	string name {
		pos			"0 0"
		color		"0 .78 0 1"
		size		"260 25"
		onClick {
			cmd "equipment_soldierlist_select <node:parent.id@integer>;"
		}
	}
}

window equipment extends objectinfo
{
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	// ==================
	// background
	// ==================

	image background
	{
		src	"background/equipment_bg"
	}

	string s_backpack
	{
		pos "852 25"
		contentalign ALIGN_UC
		string	"_Backpack"
		color "0 0.6 0 1"
		size "160 25"
	}

	string s_left
	{
		pos "852 150"
		contentalign ALIGN_UC
		string	"_Left Hand"
		color "0 0.6 0 1"
		size "160 25"
	}

	string s_right
	{
		pos "530 150"
		contentalign ALIGN_UC
		string	"_Right Hand"
		color "0 0.6 0 1"
		size "160 25"
	}

	string s_implant
	{
		pos "530 76"
		contentalign ALIGN_UC
		string	"_Implant"
		color "0 0.6 0 1"
		size "160 25"
	}

	string s_headgear
	{
		pos "620 76"
		contentalign ALIGN_UC
		string	"_Headgear"
		color "0 0.6 0 1"
		size "160 25"
	}

	string s_holster
	{
		pos "852 245"
		contentalign ALIGN_UC
		string	"_Holster"
		color "0 0.6 0 1"
		size "160 25"
	}

	string s_belt
	{
		pos "530 245"
		contentalign ALIGN_UC
		string	"_Belt"
		color "0 0.6 0 1"
		size "160 25"
	}

	// ==================
	// names list
	// ==================

	data selected {
	}

	data param {
	}

	rows soldierlist_lines {
		pos			"55 18"
		size		"260 320"
		color1		"0 0.08 0 1"
		color2		"0 0.16 0 1"
		lineheight	25
	}
	panel soldierlist {
		pos			"55 18"
		size		"260 320"
		padding		6
		layout		LAYOUT_TOP_DOWN_FLOW
		wheelscrollable	true

		onViewChange {
			*node:parent.soldierlist_scroll@fullsize = <fullsize>
			*node:parent.soldierlist_scroll@current = <viewpos>
			*node:parent.soldierlist_scroll@viewsize = <viewsize>
		}
	}
	vscrollbar soldierlist_scroll {
		image		"ui/scrollbar_v_green"
		pos			"317 18"
		height		"320"
		current		0
		viewsize	8
		fullsize	50

		onChange {
			*node:parent.soldierlist@viewpos = <current>
		}
	}

	confunc equipment_soldierlist_clear {
		call *node:parent.soldierlist@removeAllChild
	}
	confunc equipment_soldierlist_add {
		call *node:parent.soldierlist@createChild("member<1>", "equipSoldier")
		*node:parent.soldierlist.member<1>.id@integer = <1>
		*node:parent.soldierlist.member<1>.name@string = <2>

		if ( *node:root.selected@integer < 0 ) {
			cmd "equipment_soldierlist_select <1>;"
			*node:root.selected@integer = <1>
		}
	}
	confunc equipment_soldierlist_select {
		if ( *node:root.selected@integer >= 0 ) {
			*node:parent.soldierlist.member<node:root.selected@integer>.name@color = "0 .78 0 1"
		}
		*node:root.selected@integer = <1>
		*cvar:mn_ucn = <1>
		if ( *node:root.selected@integer >= 0 ) {
			*node:parent.soldierlist.member<1>.name@color = "1 1 1 1"
			*node:root.load_warnings@integer = 0
			cmd "ui_team_select_ucn <node:root.selected@integer>;"
			cmd "mn_updateactorload equip_actorload;"
		}
	}

	button bt_deequipsoldier {
		string "_Remove all"
		pos		"348 305"
		size	"180 35"
		background	"ui/button_blue"
		color	"1 1 1 0.8"
		selectcolor "1 1 1 1"

		onClick	{
			cmd "ui_team_deequip <node:root.selected@integer>;update_item_list;"
			cmd "mn_updateactorload equip_actorload <node:root.load_warnings@integer>;"
		}
	}

	// ==================
	// category buttons
	// ==================

	confunc update_item_list {
		if ( *cvar:mn_equiptype == 0 ) {
			*node:root.equip@filter = FILTER_S_PRIMARY
			*node:root.equip_ammo@filter = FILTER_S_PRIMARY
		}
		elif ( *cvar:mn_equiptype == 1 ) {
			*node:root.equip@filter = FILTER_S_SECONDARY
			*node:root.equip_ammo@filter = FILTER_S_SECONDARY
		}
		elif ( *cvar:mn_equiptype == 2 ) {
			*node:root.equip@filter = FILTER_S_HEAVY
			*node:root.equip_ammo@filter = FILTER_S_HEAVY
		}
		elif ( *cvar:mn_equiptype == 3 ) {
			*node:root.equip@filter = FILTER_S_MISC
			*node:root.equip_ammo@filter = FILTER_S_MISC
		}
		elif ( *cvar:mn_equiptype == 4 ) {
			*node:root.equip@filter = FILTER_S_ARMOUR
			*node:root.equip_ammo@filter = FILTER_S_ARMOUR
		}
		elif ( *cvar:mn_equiptype == 5 ) {
			*node:root.equip@filter = FILTER_S_IMPLANT
			*node:root.equip_ammo@filter = FILTER_S_IMPLANT
		}
		*node:root.equip@viewpos = 0
		*node:root.equip_ammo@viewpos = 0
	}

	tab equip_type
	{
		{
			pos		"400 365"
			size	"505 40"
			cvar	*cvar:mn_equiptype
			onChange {
				cmd	"update_item_list;"
			}
		}

		option primary {
			label	"_Primary"
			value	"0"
		}
		option secondary {
			label	"_Secondary"
			value	"1"
		}
		option heavy {
			label	"_Heavy"
			value	"2"
		}
		option misc {
			label	"_Misc"
			value	"3"
		}
		option armour {
			label	"_Armour"
			value	"4"
		}
	}

	// ==================
	// item description
	// ==================

	panel itemdescription {
		{
			pos		"30 405"
			size	"350 336"
		}

		string itemname
		{
			string	*cvar:mn_itemname
			pos		"10 6"
			size	"330 20"
			contentalign	ALIGN_UC
		}

		item itemmodel
		{
			pos 		"0 30"
			size 		"350 100"
			src			*cvar:mn_item
			angles		"0 180 90"
			autoscale	true
		}

		text description
		{
			pos			"10 140"
			size		"310 150"
			lineheight	20
			tabwidth	190
			rows		"7"
			dataid		TEXT_ITEMDESCRIPTION
			onViewChange {
				*node:root.itemdescription.description_scroll@fullsize = <fullsize>
				*node:root.itemdescription.description_scroll@current = <viewpos>
			}
			onWheel {
				*node:root.itemdescription.description_scroll@current = <viewpos>
			}
		}
		vscrollbar description_scroll {
			image		"ui/scrollbar_v_green"
			pos			"325 140"
			height		"150"
			current		0
			viewsize	7
			fullsize	7
			hidewhenunused true
			onChange {
				*node:root.itemdescription.description@viewpos = <current>
			}
		}

		// ======================
		// useable weapon or ammo
		// ======================

		string header_item
		{
			string	"_With:"
			pos		"10 290"
			size	"100 16"
			font	"f_small"
		}

		button action_dec
		{
			pos		"120 290"
			icon	"icons/prev"
			size	"16 16"
			onClick	{ cmd "mn_decreaseitem;" }
		}

		string item_name
		{
			string	*cvar:mn_linkname
			pos		"140 290"
			size	"190 16"
			contentalign	ALIGN_CC
			font	"f_small"
		}

		button action_inc
		{
			pos		"334 290"
			icon	"icons/next"
			size	"16 16"
			onClick	{ cmd "mn_increaseitem;" }
		}

		// ==================
		// firemode
		// ==================

		string header_firemode
		{
			string	"_Firemode:"
			pos		"10 314"
			size	"100 16"
			font	"f_small"
		}

		button firemode_dec
		{
			tooltip "_Previous firemode"
			pos		"120 314"
			icon	"icons/prev"
			size	"16 16"
			onClick	{ cmd "mn_decreasefiremode;" }
		}

		string firemode_name
		{
			string	*cvar:mn_firemodename
			pos		"140 314"
			size	"190 16"
			contentalign	ALIGN_CC
			font	"f_small"
		}

		button firemode_inc
		{
			tooltip "_Next firemode"
			pos		"334 314"
			icon	"icons/next"
			size	"16 16"
			onClick	{ cmd "mn_increasefiremode;" }
		}
	}

	// ==================
	// actor stats
	// ==================
	panel actor_statpanel {
		{
			pos		"30 405"
			size	"350 336"
		}

		image pwr_bar_full
			{
				pos "30 27"
				src		"ui/bar_attribute_disabled"
			}
		cmpDefNamedTbar pwr {
			{
				pos "4 9"
			}
			tbar bar   { current *cvar:mn_vpwr }
			string lbl { string "_Strength:" }
			string val { string *cvar:mn_tpwr }
		}

		image spd_bar_full
			{
				pos "30 62"
				src		"ui/bar_attribute_disabled"
			}
		cmpDefNamedTbar spd {
			{
				pos "4 44"
			}
			tbar bar   { current *cvar:mn_vspd }
			string lbl { string "_Speed:" }
			string val { string *cvar:mn_tspd }
		}

		image acc_bar_full
			{
				pos "30 97"
				src		"ui/bar_attribute_disabled"
			}
		cmpDefNamedTbar acc {
			{
				pos "4 79"
			}
			tbar bar   { current *cvar:mn_vacc }
			string lbl { string "_Accuracy:" }
			string val { string *cvar:mn_tacc }
		}

		image mnd_bar_full
			{
				pos "30 132"
				src		"ui/bar_attribute_disabled"
			}
		cmpDefNamedTbar mnd {
			{
				pos "4 114"
			}
			tbar bar   { current *cvar:mn_vmnd }
			string lbl { string "_Mind:" }
			string val { string *cvar:mn_tmnd }
		}

		image cls_bar_full
			{
				pos "30 175"
				src		"ui/bar_skill_disabled"
			}
		cmpDefNamedTbar cls {
			{
				pos "4 157"
			}
			tbar bar   { current *cvar:mn_vcls image "ui/bar_skill" }
			string lbl { string "_Close Combat:" }
			string val { string *cvar:mn_tcls }
		}

		image ass_bar_full
			{
				pos "30 210"
				src		"ui/bar_skill_disabled"
			}
		cmpDefNamedTbar ass {
			{
				pos "4 192"
			}
			tbar bar   { current *cvar:mn_vass image "ui/bar_skill" }
			string lbl { string "_Assault Weapons:" }
			string val { string *cvar:mn_tass }
		}

		image snp_bar_full
			{
				pos "30 245"
				src		"ui/bar_skill_disabled"
			}
		cmpDefNamedTbar snp {
			{
				pos "4 227"
			}
			tbar bar   { current *cvar:mn_vsnp image "ui/bar_skill" }
			string lbl { string "_Sniper Rifles:" }
			string val { string *cvar:mn_tsnp }
		}

		image exp_bar_full
			{
				pos "30 280"
				src		"ui/bar_skill_disabled"
			}
		cmpDefNamedTbar exp {
			{
				pos "4 262"
			}
			tbar bar   { current *cvar:mn_vexp image "ui/bar_skill" }
			string lbl { string "_High-Explosives:" }
			string val { string *cvar:mn_texp }
		}
	}

	// ==================
	// switch
	// ==================

	// @todo merge with confunc team_weapon_stats?
	func weapon_stats
	{
		if ( *cvar:mn_itemname ne "" ) {
			*node:root.itemdescription@invis = false
			*node:root.actor_statpanel@invis = true
		} else {
			// switch back to actor view
			*cvar:mn_infotype = 0
		}
	}

	// @todo merge with confunc team_actor_stats?
	func actor_stats
	{
		*node:root.itemdescription@invis = true
		*node:root.actor_statpanel@invis = false
	}

	tab info {
		{
			pos		"20 365"
			size	"355 40"
			cvar	*cvar:mn_infotype
			onChange {
				if ( *cvar:mn_infotype eq 0 ) {
					call *node:root.actor_stats
				} elif ( *cvar:mn_infotype eq 1 ) {
					call *node:root.weapon_stats
				}
			}
		}
		option abilities {
			label	"_Soldier Abilities"
			value	"0"
		}
		option weaponinfo {
			label	"_Object Info"
			value	"1"
		}
	}

	// ==================
	// inventory
	// ==================

	textentry name
	{
		string		*cvar:mn_name
		tooltip		"_Rename your unit"
		pos			"544 24"
		size		"230 20"
		onChange {
			cmd "employee_changename;"
			cmd "ui_team_fillequip <node:root.param@string>"
			cmd "update_item_list;"
			cmd "equipment_soldierlist_select <node:root.selected@integer>;"
		}
	}

	button bt_edit
	{
		tooltip		"_Rename your unit"
		icon		"icons/pen"
		pos			"781 16"
		size		"35 35"
		padding		0
		onClick		{ call *node:parent.name@edit }
	}

	model body
	{
		{
			src		*cvar:mn_body
			skin	*cvar:mn_body_skin
			anim	"stand_menu"
			angles	"0 105 90"
			scale	"4 4 4"
			pos		"675 75"
			size	"197 258"
			origin	"0 -30 0"
		}

		model head
		{
			src		*cvar:mn_head
			skin	*cvar:mn_head_skin
			tag		"tag_head"
			scale	"1.0 1.0 1.0"
		}
	}

	/**
	 * @sa CL_InitSkin_f
	 * @sa team_initskin
	 */
	selectbox skins {
		pos		"660 290"
		size	"120 40"
		cvar	*cvar:mn_body_skin
		// TODO: ui_team_changeskin is only available in the campaign cgame
		onChange { cmd "ui_team_changeskin <node:root.selected@integer> <cvar:mn_body_skin>;" }
		dataid	OPTION_SINGLEPLAYER_SKINS
	}

	// ==================
	// Containers (see inventory.ufo for the definition)
	// ==================

	container implant	{ pos "548 104"	onSelect { cmd "object_update <lastselectedid> update_item_list;" } }
	container right		{ pos "548 178"	onSelect { cmd "object_update <lastselectedid> update_item_list;" } }
	container left		{ pos "872 178"	onSelect { cmd "object_update <lastselectedid> update_item_list;" } }
	container backpack	{ pos "872 54"	onSelect { cmd "object_update <lastselectedid> update_item_list;" } }
	container belt		{ pos "569 273"	onSelect { cmd "object_update <lastselectedid> update_item_list;" } }
	container holster	{ pos "898 270"	onSelect { cmd "object_update <lastselectedid> update_item_list;" } }
	container headgear	{ pos "678 104"	onSelect { cmd "object_update <lastselectedid> update_item_list;" } }

	container armour	{
		pos "735 100"
		color "1 1 1 0"		// active, but display it transparent
		onChange { cmd "equipment_soldierlist_select <node:root.selected@integer>;" }
		onSelect { cmd "object_update <lastselectedid> update_item_list;" }
	}

	baseinventory equip		{
		pos						"422 415"
		size					"207 317"
		displayweapon			true
		displayammoofweapon		true
		displayunavailableitem	true
		columns					1
		onViewChange {
			*node:root.equip_scroll@current = <viewpos>
			*node:root.equip_scroll@viewsize = <viewsize>
			*node:root.equip_scroll@fullsize = <fullsize>
		}
		onSelect { cmd "object_update <lastselectedid> update_item_list 0;" }
		onChange { cmd "mn_updateactorload equip_actorload <node:root.load_warnings@integer>;" }
	}

	baseinventory equip_ammo	{
		pos				"702 418"
		size			"207 317"
		displayammo		true
		columns			2
		onViewChange {
			*node:root.equip_ammo_scroll@current = <viewpos>
			*node:root.equip_ammo_scroll@viewsize = <viewsize>
			*node:root.equip_ammo_scroll@fullsize = <fullsize>
		}
		onSelect { cmd "object_update <lastselectedid> update_item_list 0;" }
		onChange { cmd "mn_updateactorload equip_actorload <node:root.load_warnings@integer>;" }
	}

	vscrollbar equip_scroll {
		image	"ui/scrollbar_v_green"
		pos "629 405"	height	"339"
		hidewhenunused true
		onChange {
			*node:root.equip@viewpos = <current>
		}
	}

	vscrollbar equip_ammo_scroll {
		image	"ui/scrollbar_v_green"
		pos "892 405"	height	"339"
		hidewhenunused true
		onChange {
			*node:root.equip_ammo@viewpos = <current>
		}
	}

	button bt_load
	{
		pos "604 311"
		contentalign ALIGN_CL
		string	"_0/0 Kg"
		color "1 1 1 1"
		flashcolor "1 0 0 1"
		size "285 22"
		onClick { cmd "mn_updateactorload equip_actorload;" }
	}

	data load_warnings {
		integer 0
	}

	confunc equip_actorload {
		*node:root.bt_load@string = "<1>"
		*node:root.bt_load@tooltip = "<2>"
		*node:root.load_warnings@integer = "<4>"
		if ( <3> > 0 ) { *node:root.bt_load@color = "0 1 0 1" }
		elif ( <3> < 0 ) { *node:root.bt_load@color = "1 1 0 1" }
		else { *node:root.bt_load@color = "1 1 1 1" }
		if ( <4> > 0 ) { *node:root.bt_load@flash = true }
		else { *node:root.bt_load@flash = false }
	}

	// ==================
	// additional stuff
	// ==================
	button bt_go {
		tooltip			"_Done"
		background		"buttons/large_blue"
		pos				"940 720"
		size			"64 37"
		icon			"icons/return"
		onClick {
			cmd "ui_pop;"
		}
	}

	func onWindowClosed {
		/** @note Do not define action here
		 * this close is edited by the code
		 * pushing this menu
		 */
	}

	func onWindowOpened {
		call *node:root.actor_stats
		*cvar:mn_infotype = 0
		*cvar:mn_equiptype = 0
		*cvar:mn_linkname = ""
		*cvar:mn_itemname = ""
		*node:root.selected@integer = -1
		*node:root.param@string = ""

		cmd "team_initskin;"
		if ( paramCount > 0 ) {
			*node:root.param@string = <1>
			cmd "ui_team_fillequip <1>;"
		} else {
			cmd "ui_team_fillequip;"
		}
		cmd "update_item_list;"
	}
}
