// ==================
// SINGLE PLAYER MENU
// ==================

component cmpGameModeButton extends cmpMenuButton
{
	{
		pos		"0 0"
		size	"256 64"
		align	LAYOUTALIGN_TOP
	}

	string id
	{
		size	"0 0"
		invis	true
	}
}

window singleplayer
{
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	// ==================
	// background
	// ==================

	image background
	{
		src		"background/main_bg"
	}

	string version
	{
		string		*cvar:version
		pos			"10 744"
		size		"550 20"
	}

	// ==================
	// buttons
	// ==================

	/**
	 * @param <1> window id
	 * @param <2> name
	 */
	confunc game_addmode_singleplayer
	{
		call *node:root.gamemodeslistpanel@createChild ( "<1><2>" , "cmpGameModeButton" )
		*node:root.gamemodeslistpanel.<1><2>.id@string = <1>
		*node:root.gamemodeslistpanel.<1><2>@string = <2>
		*node:root.gamemodeslistpanel.<1><2>@onClick = { cmd "ui_push <node:this.id@string>;" }
	}

	panel gamemodeslistpanel {
		{
			pos				"640 170"
			size			"256 276"
			align			LAYOUTALIGN_LEFT
			layout			LAYOUT_PACK
			ghost			true
			layoutmargin	5
		}
	}

	cmpMenuButton button_back
	{
		string		"_Back"
		tooltip		"_Back to the main menu."
		pos			"640 520"
		onClick		{ cmd "ui_pop;" }
	}
}

// ==================
// SINGLE PLAYER MISSION MENU
// ==================

window singleplayermission
{
	/** @todo Remove me */
	{ } // empty properties to fix parsing problem with "image" token (behaviour or property?)

	// ==================
	// background
	// ==================

	image background
	{
		src		"background/main_bg"
	}

	string version
	{
		string		*cvar:version
		pos			"10 744"
		size		"550 20"
	}

	// ==================
	// buttons
	// ==================

	cmpMenuButton button_continue
	{
		string		"_Continue"
		tooltip		"_Go back to the battlescape."
		pos			"640 170"
		onClick		{ cmd "ui_pop;" }
	}

	cmpMenuButton button_abort
	{
		string		"_Abort"
		tooltip		"_Abort this battlescape mission."
		pos			"640 240"
		onClick		{ cmd "game_abort;" }
	}

	cmpMenuButton button_retry
	{
		string		"_Retry"
		tooltip		"_Retry this battlescape mission."
		pos			"640 310"
		onClick		{ cmd "game_go;" }
	}

	cmpMenuButton button_options
	{
		string		"_Options"
		tooltip		"_Enter the game options menu."
		pos			"640 380"
		onClick		{ cmd "ui_push options;" }
	}

	cmpMenuButton button_exit_battlescape
	{
		string		"_Exit"
		tooltip		"_You will lose all your progress."
		pos			"640 520"
		onClick		{ cmd "check_if_in_campaign_mode;" }
	}

	confunc check_if_in_campaign_mode
	{
		if ( *cvar:mn_base_count > 0 ) { cmd "ui_push popup_exit_campaign_confirmation;" }
		else { cmd "game_exit;" }
	}
}

// ========================
// POPUP EXIT CONFIRMATION
// ========================
window popup_exit_campaign_confirmation
{
	{
		string		"_Exit Campaign Confirmation"
		background		"ui/popup_alpha_tile"
		pos			"264 262"
		size		"486 247"
		dragbutton	true
		closebutton	true
	}
	string exit_confirmation_label
	{
		string		"_You will lose all your campaign progress."
		pos			"26 58"
		size		"450 60"
	}
	cmpPopupButton bt_quit
	{
		tooltip		"_Exit current campaign and return to main menu."
		string		"_Quit"
		pos			"28 188"
		onClick		{ cmd "game_exit;" }
	}
	cmpCancelButton bt_dontquit
	{
		tooltip		"_Do not quit."
		pos			"285 188"
		onClick		{ cmd "ui_pop;" }
	}
}
