//

component cmp_soldierentry extends panel {
	{
		size		"310 25"
	}

	data id {}

	string name {
		pos			"0 0"
		color		"0 .78 0 1"
		size		"270 25"
		onClick {
			cmd "soldierlist_select <node:parent.id@integer>;"
		}
	}

	checkbox_green selected {
		tooltip		"_Add/remove soldier to the team"
		pos			"275 0"
		size		"25 25"

		onChange {
			cmd "ui_team_assign_ucn <node:parent.id@integer>; ui_team_fillbdef; soldierlist_select <node:parent.id@integer>;"
		}
	}
}

window popup_assembleteam
{
	{
		pos				"0 256"
		size			"1024 445"
		dragbutton		true
		closebutton		true
		modal			true				// we can't click out
		background			"ui/popup_alpha_tile"
		string			"_Assemble Team"
	}

	data selected {
	}

	rows soldierlist_lines {
		pos				"35 66"
		size			"310 299"
		color1			"0 0.08 0 1"
		color2			"0 0.16 0 1"
		lineheight		25
	}
	panel soldierlist {
		{
			pos			"35 60"
			size		"310 312"
			layout		LAYOUT_TOP_DOWN_FLOW
			padding		6
			wheelscrollable	true

			onViewChange	{
				*node:parent.soldierlist_scroll@fullsize = <fullsize>
				*node:parent.soldierlist_scroll@current = <viewpos>
				*node:parent.soldierlist_scroll@viewsize = <viewsize>
			}
		}
	}
	vscrollbar soldierlist_scroll {
		image		"ui/scrollbar_v_green"
		pos			"350 60"
		height		"312"
		hidewhenunused	true
		onChange	{
			*node:parent.soldierlist@viewpos = <current>
		}
	}

	panel soldiermodel {
		{
			pos		"375 70"
			size	"179 300"
			background	"ui/panel_green"
		}

		image	rank_img {
			pos "8 8"
			size "50 50"
			src *cvar:mn_chrrank_img
		}

		model body {
			{
				src		*cvar:mn_body
				skin	*cvar:mn_body_skin
				anim	"stand2"
				angles	"0 70 90"
				scale	"5.5 5.5 5.5"
				pos		"6 6"
				size	"168 283"
				origin	"0 -35 0"
			}
			model head {
				src		*cvar:mn_head
				skin	*cvar:mn_head_skin
				tag		"tag_head"
				scale	"1.0 1.0 1.0"
			}
			model right_rweapon {
				src		*cvar:mn_rweapon
				tag		"tag_rweapon"
			}
			model right_lweapon {
				src		*cvar:mn_lweapon
				tag		"tag_lweapon"
			}
		}

		string	name {
			string		*cvar:mn_name
			pos			"8 247"
			size		"165 20"
		}
		string	rank_lbl {
			string		*cvar:mn_chrrank
			pos			"8 267"
			size		"165 20"
		}
	}

	panel soldierstat {
		{
			pos			"555 70"
			size		"445 300"
		}

		tab actor_stat {
			{
				pos		"0 0"
				size	"438 39"
				cvar	*cvar:mn_actor_stat
				onChange {
					call *node:parent.select_actor_stat
				}
			}
			option phys {
				label	"_Physical stats"
				value	"stats_phys"
			}
			option missions {
				label	"_Missions info"
				value	"stats_missions"
			}
		}

		func select_actor_stat {
			if ( *cvar:mn_actor_stat eq "stats_phys" ) {
				*node:parent.stats_phys@invis = false
				*node:parent.stats_missions@invis = true
			} elif ( *cvar:mn_actor_stat eq "stats_missions" ) {
				*node:parent.stats_phys@invis = true
				*node:parent.stats_missions@invis = false
			}
		}

		panel stats_phys {
			{
				pos		"0 33"
				size	"440 267"
				background	"ui/panel_green_tab"
			}
			cmpNamedBar pwr {
				{
					pos "9 27"
				}
				bar currbar { current *cvar:mn_vpwr }
				bar initbar { current *cvar:mn_vpwri }
				string lbl  { string "_Strength:" }
				string val  { string *cvar:mn_tpwr }
			}
			cmpNamedBar spd {
			{
				pos "9 50"
			}
				bar currbar { current *cvar:mn_vspd }
				bar initbar { current *cvar:mn_vspdi }
				string lbl  { string "_Speed:" }
				string val  { string *cvar:mn_tspd }
			}
			cmpNamedBar acc {
			{
				pos "9 73"
			}
				bar currbar { current *cvar:mn_vacc }
				bar initbar { current *cvar:mn_vacci }
				string lbl  { string "_Accuracy:" }
				string val  { string *cvar:mn_tacc }
			}
			cmpNamedBar mnd {
			{
				pos "9 96"
			}
				bar currbar { current *cvar:mn_vmnd }
				bar initbar { current *cvar:mn_vmndi }
				string lbl  { string "_Mind:" }
				string val  { string *cvar:mn_tmnd }
			}
			cmpNamedBar cls {
			{
				pos "9 119"
			}
				bar currbar { current *cvar:mn_vcls }
				bar initbar { current *cvar:mn_vclsi }
				string lbl  { string "_Close Combat:" }
				string val  { string *cvar:mn_tcls }
			}

			cmpNamedBar ass {
			{
				pos "9 142"
			}
				bar currbar { current *cvar:mn_vass }
				bar initbar { current *cvar:mn_vassi }
				string lbl  { string "_Assault Weapons:" }
				string val  { string *cvar:mn_tass }
			}

			cmpNamedBar snp {
			{
				pos "9 165"
			}
				bar currbar { current *cvar:mn_vsnp }
				bar initbar { current *cvar:mn_vsnpi }
				string lbl  { string "_Sniper Rifles:" }
				string val  { string *cvar:mn_tsnp }
			}

			cmpNamedBar exp {
			{
				pos "9 188"
			}
				bar currbar { current *cvar:mn_vexp }
				bar initbar { current *cvar:mn_vexpi }
				string lbl  { string "_High-Explosives:" }
				string val  { string *cvar:mn_texp }
			}

			cmpNamedTbar hp {
			{
				pos "9 211"
			}
				tbar bar   {
					current *cvar:mn_vhp
					max *cvar:mn_vhpmax
					image "ui/bar_health"
					}
				string lbl { string "_Health:" }
				string val { string *cvar:mn_thp }
			}

			}

			panel stats_missions {
				{
					pos		"0 33"
					size	"440 267"
					background	"ui/panel_green_tab"
					invis true
				}

				image	rank_img {
					pos "195 30"
					size "50 50"
					src *cvar:mn_chrrank_img
				}

			string	rank_lbl { pos "10 85" size "420 20" string *cvar:mn_chrrank contentalign ALIGN_UC font "f_normal_bold" }

			string	missions_lbl { pos "12 150" size "200 20" string "_Missions performed:" }
			string	missions_val { pos "212 150" size "150 20" string *cvar:mn_chrmis contentalign ALIGN_UR }

			string	kills_lbl { pos "12 170" size "200 20" string "_Enemies killed:" }
			string	kills_val { pos "212 170" size "150 20" string *cvar:mn_chrkillalien contentalign ALIGN_UR }

			string	civ_lbl { pos "12 190" size "200 20" string "_Civilians killed:" }
			string	civ_val { pos "212 190" size "150 20" string *cvar:mn_chrkillcivilian contentalign ALIGN_UR }

			string	team_lbl { pos "12 210" size "200 20" string "_Our team killed:" }
			string	team_val { pos "212 210" size "150 20" string *cvar:mn_chrkillteam contentalign ALIGN_UR }
		}
	}

	cmpPopupButton equip {
		string		"_Equip"
		pos			"350 380"
		disabled	true

		onClick {
			cmd "ui_push equipment;"
		}
	}

	cmpPopupButton close {
		string		"_Close"
		pos			"550 380"

		onClick {
			cmd "ui_pop;"
		}
	}

	confunc soldierlist_clear {
		call *node:parent.soldierlist@removeAllChild
	}
	confunc soldierlist_add {
		call *node:parent.soldierlist@createChild("soldier<1>", "cmp_soldierentry")
		*node:parent.soldierlist.soldier<1>.id@integer = <1>
		*node:parent.soldierlist.soldier<1>.name@string = <2>
		*node:parent.soldierlist.soldier<1>.selected@current = <3>

		 if ( *cvar:cpteam_size > 0 ) {
			*node:parent.equip@disabled = false
		} else {
			*node:parent.equip@disabled = true
		}

		if ( "<4>" ne "" ) {
			*node:parent.soldierlist.soldier<1>.selected@tooltip = <4>
			*node:parent.soldierlist.soldier<1>.selected@disabled = true
		}

		if ( *node:root.selected@integer < 1 ) {
			cmd "soldierlist_select <1>;"
			*node:root.selected@integer = <1>
		}
	}
	confunc soldierlist_select {
		if ( *node:root.selected@integer >= 0 ) {
			*node:parent.soldierlist.soldier<node:root.selected@integer>.name@color = "0 .78 0 1"
		}
		*node:root.selected@integer = <1>
		if ( *node:root.selected@integer >= 0 ) {
			*node:parent.soldierlist.soldier<1>.name@color = "1 1 1 1"
			cmd "ui_team_select_ucn <node:root.selected@integer>;"
		}
	}

	func onWindowOpened {
		*node:root.selected@integer = -1
		cmd	"cp_missionauto_check; ui_team_fillbdef"
	}
	func onWindowClosed {
		cmd "popbaseattack_upd;"
	}
}


window popup_baseattack
{
	{
		pos			"264 256"
		size		"496 230"
		dragbutton	true
		modal		true				// we can't click out
		preventtypingescape true		// we can't use ESC
		background		"ui/popup_alpha_tile"
		string		"_Base is under attack"
	}

	text description {
		pos			"26 65"
		dataid		TEXT_POPUP
		size		"450 48"
		lineheight	16
	}

	checkbox_blue killaliens {
		current		0
		pos			"26 120"
		size		"20 18"
	}
	string killaliens_str {
		string		"_Kill aliens in container"
		pos			"55 120"
		size		"220 20"
	}
	checkbox_blue liftcrafts {
		current		0
		disabled	true
		tooltip		"_Not implemented"
		pos			"26 145"
		size		"20 18"
	}
	string liftcrafts_str {
		string		"_Lift aircraft"
		disabled	true
		tooltip		"_Not implemented"
		pos			"55 145"
		size		"220 20"
	}
	checkbox_blue powerdown {
		current		0
		disabled	true
		tooltip		"_Not implemented"
		pos			"26 170"
		size		"20 18"
	}
	string powerdown_str {
		string		"_Disable Power Plant"
		disabled	true
		tooltip		"_Not implemented"
		pos			"55 170"
		size		"220 20"
	}

	func listen_mission {
		cmd "game_go;"
		cmd "ui_removelistener equipment@onWindowClosed <path:this>;"
	}

	cmpPopupButton bt_assign {
		string		"_Team"
		tooltip		"_Assemble defend team"
		pos			"290 120"

		onClick {
			cmd "ui_push popup_assembleteam;"
		}
	}

	cmpPopupButton bt_go {
		string		"_Enter"
		tooltip		"_Enter the battlefield"
		pos			"290 160"
		disabled	true

		onClick		{
			if ( *node:root.liftcrafts@current > 0 ) {
				cmd "cp_aircraft_liftall <baseid>;"
			}
			if ( *node:root.killaliens@current > 0 ) {
				cmd "cp_aliencont_killall <baseid>;"
			}
			if ( *node:root.powerdown@current > 0 ) {
				cmd "cp_powerdown <baseid>;"
			}
			cmd "game_go;"
		}
	}

	confunc popbaseattack_upd {
		if ( *cvar:cpteam_size > 0 ) {
			*node:parent.bt_go@disabled = false
			*node:parent.bt_go@tooltip = "_Enter the battlefield"
		} else {
			*node:parent.bt_go@disabled = true
			*node:parent.bt_go@tooltip = "_Assemble a team first"
		}
	}

	func onWindowOpened	{
		*cvar:cpteam_size = 0
		cmd	"cp_missionauto_check;"
		cmd "popbaseattack_upd;"
	}
}
