// ==================
// MENU FOR PAUSE/MESSAGES
// ==================

component cmpMessageOptions extends panel {
	{
		size	"90 20"
		invis	true
	}

	checkbox_blue notify {
		tooltip		"_enable or disable displaying a notification message"
		pos			"0 0"
		size		"20 18"
		current		0
		onChange	{ cmd "msgoptions_toggle <node:parent@num> notify;" }
	}

	checkbox_blue pause {
		tooltip		"_enable or disable game stopping"
		pos			"30 0"
		size		"20 18"
		current		0
		onChange	{ cmd "msgoptions_toggle <node:parent@num> pause;" }
	}

	checkbox_blue sound {
		tooltip		"_enable or disable playing sound"
		pos			"60 0"
		size		"20 18"
		current		0
		onChange	{ cmd "msgoptions_toggle <node:parent@num> sound;" }
	}
}

window messageoptions
{
	{
		pos			"256 256"
		size		"496 496"
		dragbutton	true
		closebutton	true
		modal		true
		background		"ui/popup_alpha_tile"
		string		"_Pause/message settings"
	}

	cmpPopupButton button_apply
	{
		string		"_Apply"
		tooltip		"_Apply changes"
		pos			"109 436"
		onClick		{ cmd "ui_pop;msgoptions_backup;" }
	}

	cmpCancelButton button_revert
	{
		tooltip		"_Revert changes"
		pos			"296 436"
		onClick		{ cmd "ui_pop;" }
	}

	string header_messagetype
	{
		string		"_Notify types:"
		pos			"30 60"
		size		"300 25"
		font		"f_small_bold"
	}

	button header_notify
	{
		tooltip		"_Enable or disable messages being displayed for all notification types.\nLeft click enables all notifications, right click disables."
		pos			"356 60"
		size		"26 20"
		padding		0
		icon		"icons/notify_notice"
		onClick		{ cmd "msgoptions_setall notify 1;" }
		onRClick		{ cmd "msgoptions_setall notify 0;" }
	}

	button header_pause
	{
		tooltip		"_Enable or disable pause for all notification types\nPause can't be enabled without notification\nLeft click enables pause for all, right click disables."
		pos			"386 60"
		size		"26 20"
		icon		"icons/notify_pause"
		padding		0
		onClick		{ cmd "msgoptions_setall pause 1;" }
		onRClick		{ cmd "msgoptions_setall pause 0;" }
	}

	button header_sound
	{
		tooltip		"_Enable or disable sounds being played for all notification types.\nLeft click enables all sounds, right click disables."
		pos			"416 60"
		size		"26 20"
		icon		"icons/notify_sound"
		padding		0
		onClick		{ cmd "msgoptions_setall sound 1;" }
		onRClick		{ cmd "msgoptions_setall sound 0;" }
	}

	rows lines {
		pos			"30 90"
		size		"420 325"
		color1		"0 0.08 0 1"
		color2		"0 0.16 0 1"
		lineheight	25
	}

	optiontree messagetypes
	{
		cvar		*cvar:mn_messagetype
		dataid		TEXT_MESSAGEOPTIONS
		color		"0 .78 0 1"
		selectcolor "0 .78 0 1"
		lineheight	"25"
		padding		0
		pos			"30 90"
		size		"325 325"
		onViewChange {
			*node:root.messagetypes_scroll@fullsize = <fullsize>
			*node:root.messagetypes_scroll@current = <viewpos>
			*node:root.messagetypes_scroll@viewsize = <viewsize>
			cmd "msgoptions_scroll <viewpos>;"
		}
	}

	vscrollbar messagetypes_scroll {
		image		"ui/scrollbar_v_green"
		pos			"455 90"
		height		"325"
		current		0
		viewsize	13
		fullsize	13
		onChange	{
			*node:root.messagetypes@viewpos = <current>
			cmd "msgoptions_scroll <current>;"
		}
	}

	// ===================
	// All checkbox
	// ===================

	cmpMessageOptions option0 {
		pos		"360 94"
		num		0
	}

	cmpMessageOptions option1 {
		pos		"360 119"
		num		1
	}

	cmpMessageOptions option2 {
		pos		"360 144"
		num		2
	}

	cmpMessageOptions option3 {
		pos		"360 169"
		num		3
	}

	cmpMessageOptions option4 {
		pos		"360 194"
		num		4
	}

	cmpMessageOptions option5 {
		pos		"360 219"
		num		5
	}

	cmpMessageOptions option6 {
		pos		"360 244"
		num		6
	}

	cmpMessageOptions option7 {
		pos		"360 269"
		num		7
	}

	cmpMessageOptions option8 {
		pos		"360 294"
		num		8
	}

	cmpMessageOptions option9 {
		pos		"360 319"
		num		9
	}

	cmpMessageOptions option10 {
		pos		"360 344"
		num		10
	}

	cmpMessageOptions option11 {
		pos		"360 369"
		num		11
	}

	cmpMessageOptions option12 {
		pos		"360 394"
		num		12
	}

	cmpMessageOptions option13 {
		pos		"360 419"
		num		13
	}

	// ===================
	// confuncs to update widgets
	// ===================

	/**
	 * @param[in] <1> row in current view
	 * @param[in] <2> state for pause button
	 * @param[in] <3> state for notification button
	 * @param[in] <4> state for sound notification button
	 */
	confunc ms_btnstate {
		*cvar:tmp = "<3>"
		if ( *cvar:tmp == 0 ) {
			*node:parent.option<1>.pause@disabled = true
			*node:parent.option<1>.sound@disabled = true
		} else {
			*node:parent.option<1>.pause@disabled = false
			*node:parent.option<1>.sound@disabled = false
		}
		*node:parent.option<1>.pause@current = <2>
		*node:parent.option<1>.notify@current = <3>
		*node:parent.option<1>.sound@current = <4>
		delete *cvar:tmp
	}

	// ===================
	// confuncs to enable/disable lines
	// ===================

	/**
	 * @param[in] <1> row into the current view
	 */
	confunc ms_enable {
		*node:root.option<1>@invis = false
	}

	/**
	 * @param[in] <1> row into the current view
	 */
	confunc ms_disable {
		*node:root.option<1>@invis = true
	}

	func onWindowOpened
	{
		*cvar:mn_messagetype = -1
		*node:root.messagetypes@viewpos = 0
		*node:root.messagetypes_scroll@current = 0
		cmd "game_timestop;msgoptions_init 13;msgoptions_backup;"
		cmd "msgoptions_scroll <node:root.messagetypes_scroll@current>;"
	}

	func onWindowClosed
	{
		cmd "msgoptions_restore;"
		cmd "del mn_messagetype;"
	}
}
